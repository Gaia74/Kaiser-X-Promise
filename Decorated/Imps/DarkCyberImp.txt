ACTOR DarkCyberImp 7542 
{ 
//$Category Imps
//Game Doom
  Health 140
  Radius 22
  Height 60
  Scale 1.1
  Mass 300
  Speed 13
  PainChance 100
  Monster
  +FloorClip
  +MissileMore
  +DontHarmSpecies
  Translation "48:79=[154,154,154]:[0,0,0]"
  Species "KX"
  DamageFactor "KX", 0.0
  SeeSound "KXDarkCyberImp/See"
  PainSound "KXDarkCyberImp/Pain"
  DeathSound "KXDarkCyberImp/Death"
  ActiveSound "KXDarkCyberImp/Active"
  HitObituary "%o discovered a surprise in the blade of a Dark Cyber Imp"
  Obituary "%o is burning in the flames of night by a Dark Cyber Imp"
  MeleeRange 66
  GibHealth 50
  States
  {
  Spawn:
    CIMP AB 10 A_Look
    Loop
  See:
	KCIM A 0 A_SetShootable
	KCIM A 0 A_ChangeFlag("THRUACTORS",0)
	KCIM A 0 A_TakeInventory("DarkCyberImpFiraCounter",4)
	CIMP AA 2 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/NoShoe",CHAN_7)
	CIMP BB 2 A_Chase
	CIMP CC 2 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/NoShoe",CHAN_7)
	CIMP DD 2 A_Chase
    Loop
  Melee:
    CIMP EF 4 A_FaceTarget
	TNT1 A 0 A_JumpIfCloser(56,1)
	TNT1 A 0 A_Recoil(-5)
    CIMP F 4 A_Custommeleeattack(2*random(10,40), "imp/melee") 
    Goto See
  InstaMelee:
	CIMP E 0 A_PlaySound("imp/melee")
    CIMP E 0 A_FaceTarget
	CIMP E 0 A_ScaleVelocity(0)
    CIMP F 4 A_CustomMeleeAttack(random(20,40),"imp/melee","","Melee",True)
	CIMP GG 4 A_FaceTarget
    Goto See
  Missile: 
	TNT1 A 0 A_Jump(256,"Movement","Range")
	Goto See
  Range:
	TNT1 A 0 A_PlaySound("KXDarkCyberImp/Attack",CHAN_AUTO)
    TNT1 A 0 A_Jump(224,"QuickRange")
	TNT1 A 0 A_Jump(256,"ChargingPlasma")
	Goto See
  QuickRange:
    TNT1 A 0 A_Jump(256,"Fira","Plasma")
	Goto See
  Movement:
    TNT1 A 0 A_Jump(192,"Jump","MDash")
	TNT1 A 0 A_Jump(128,"Night")
	Loop
  DecideFira:
	TNT1 A 0 A_JumpIfInventory("DarkCyberImpFiraCounter",4,"DecideFira2")
	TNT1 A 0 A_Jump(128,"Fira")
    TNT1 A 0 A_Jump(64,"Plasma","MDash")
	Goto See
  DecideFira2:
    TNT1 A 0 A_Jump(64,"Plasma","MDash")
	Goto See
  Jump:
	TNT1 A 0 A_PlaySound("KXDarkCyberImp/Attack",CHAN_VOICE,1.0)
    KCIM A 4 A_FaceTarget
	Goto Jumping
  Jumping:
	TNT1 A 0 A_PlaySound("CIMPJUMP")
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	TNT1 A 0 ThrustThingZ(0,random(25,30),0,1)
	TNT1 A 0 A_Recoil(random(-25,-35))
	TNT1 A 0 A_FaceTarget
	KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	Goto See
  EndJumping:
	TNT1 A 0 A_JumpIfTargetInsideMeleeRange("InstaMelee")
	Goto See
  MDash:
    TNT1 A 0 A_Jump(256,"MDashLeft","MDashRight")
	Goto See
  MDashLeft:
	TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_Recoil(-5)
	TNT1 A 0 ThrustThing(angle*256/360+192, 20, 0, 0)
	TNT1 A 0 A_Jump(128,"Jump","Range")
	TNT1 A 0 A_JumpIfInventory("DarkCyberImpFiraCounter",1,"DecideFira")
	Goto See
  MDashRight:
	TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_Recoil(-5)
	TNT1 A 0 ThrustThing(angle*256/360+64, 20, 0, 0)
	TNT1 A 0 A_Jump(128,"Jump","Range")
	TNT1 A 0 A_JumpIfInventory("DarkCyberImpFiraCounter",1,"DecideFira")
	Goto See
  Night:
    KCIM A 0 A_PlaySound("KXDarkCyberImp/Charge",CHAN_7,1.0)
    KCIM A 12 A_FaceTarget
	KCIM A 0 A_UnSetShootable
	KCIM A 0 A_PlaySound("KXDarkCyberImp/NightIn",CHAN_6,1.0)
	KCIM AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DarkCyberImpNightParticle",Random(24, 32), 0, frandom(1,40), 0, 0, (Random(40,90)*0.1), Random(0,360), 128)
	KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_TRANSFERTRANSLATION)
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
	KCIM A 0 A_ChangeFlag("THRUACTORS",1)
	TNT1 A 35
	TNT1 A 0 A_Jump(64,"NightEnd")
	TNT1 A 35
	TNT1 A 0 A_Jump(80,"NightEnd")
	TNT1 A 35
	TNT1 A 0 A_Jump(96,"NightEnd")
	TNT1 A 35
	TNT1 A 0 A_Jump(128,"NightEnd")
	TNT1 A 35
	Goto NightEnd
  NightEnd:
    TNT1 A 25
	TNT1 A 0 A_PlaySound("KXDarkCyberImp/Attack",CHAN_VOICE,1.0)
	TNT1 A 10 A_FaceTarget
	KCIM AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DarkCyberImpNightParticle",Random(24, 32), 0, frandom(1,40), 0, 0, (Random(40,90)*0.1), Random(0,360), 128)
	KCIM A 0 A_PlaySound("KXDarkCyberImp/NightOut",CHAN_6,1.0)
	TNT1 A 0 A_FaceTarget
	KCIM A 0 A_SetShootable
	KCIM A 0 A_ChangeFlag("THRUACTORS",0)
	Goto Jumping
  Fira:
    KCIM A 0 A_GiveInventory("DarkCyberImpFiraCounter",1)
	KCIM AB 6 A_FaceTarget
    KCIM CCCC 0 A_CustomMissile ("DarkCyberImpFireBall",32,12,frandom(-5,5))
	KCIM C 6 A_FaceTarget
	TNT1 A 0 A_Jump(80,"QuickRange")
	Goto DecideFira
  Plasma:
    CIMP E 0 A_FaceTarget
    CIMP EEEEEE 1 A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,18,25,frandom(-1,1),frandom(-1,1),frandom(-1,1))
	CIMP F 0 A_FaceTarget
	CIMP FFFFFF 1 A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,18,25,frandom(-1,1),frandom(-1,1),frandom(-1,1))
	CIMP G 0 A_Custommissile("CyberImpPlasmaBall",36,4,frandom(-1,1),CMF_OFFSETPITCH,frandom(-1,1)) 
	CIMP G 0 A_FaceTarget
	CIMP GGGGGG 1 A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,18,25,frandom(-1,1),frandom(-1,1),frandom(-1,1))
	TNT1 A 0 A_Jump(80,"QuickRange")
    Goto See
  ChargingPlasma:
	CIMP A 0 A_PlaySound("KaiserCyborg/Blaster",CHAN_AUTO)
    CIMP E 0 A_FaceTarget
    CIMP EEEEEEEEEEEEEEEEEEEE 1 A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,18,25,frandom(-1,1),frandom(-1,1),frandom(-1,1))
	CIMP F 4 A_FaceTarget
	CIMP G 8 A_Custommissile("DarkCyberImpPlasmaBall",36,4,0) 
    Goto See
  Pain:
    CIMP H 2
    CIMP H 2 A_Pain
    Goto See
  Death:
	TNT1 AAAAAAAAAA 0 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
	CIMP I 7
    CIMP J 7 A_Scream
    CIMP K 7
    CIMP L 7 A_NoBlocking
	CIMP AAAAAAAAAA 0 A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(0,2), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
    CIMP M -1
    Stop
  XDeath:
	CIMP N 0 A_CustomMissile("KXBloodExplosionEffect",37,0,0,0,0)
    CIMP N 0 A_CustomMissile("CyberDarkImpArm",32,-10,270)
	CIMP NNNNN 1 A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(10,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
    CIMP O 0 A_XScream
	CIMP OOOOOPPPPP 1  A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(10,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
    CIMP Q 5 A_NoBlocking
    CIMP RST 5
    CIMP U -1
    Stop
  Raise:
    CIMP MLKJI 8
    Goto See
  }
}

Actor DarkCyberImpFireBall
{
    Radius 4
    Height 4
    Speed 1
	Scale 0.66
    Damage (2*random(3,6))
    DamageType "Fire"
    Alpha 0.67
    Projectile
    RenderStyle Add
    SeeSound "imp/attack"
    DeathSound "KXDarkCyberImp/Fire"
    States
    {
    Spawn:
	   TNT1 A 0
       TNT1 A 0 A_ScaleVelocity(frandom(10.0,20.0))
       FDMB A 6 Bright A_SpawnItemEx("DarkCyberImpFireBallParticle",FRandom(-2,2),FRandom(-2,2),FRandom(-2,2),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
       Goto Spawn+2
    Death:
	   FDMB BBCCDDEE 1 Bright A_SetScale(ScaleX*0.95)
       Wait
    }
}

Actor DarkCyberImpFireBallParticle
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    Projectile
    RenderStyle Add
	+ClientSideOnly
	+NoInteraction
    +Randomize
	Scale 0.33
    Alpha 0.9
    States
    {
    Spawn:
		FDMB B 1 Bright A_FadeOut(0.05)
		FDMB B 0 A_SetScale(ScaleX*0.9)
		Loop
    }
}

ACTOR DarkCyberImpPlasmaBall
{
   Radius 10
   Height 20
   Speed 30
   Damage (50)
   DamageType lightning
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.75
   +THRUGHOST
   +FORCEXYBILLBOARD
   SeeSound "thor/attackr3"
   DeathSound "KXDarkCyberImp/PlasmaDeath"
   Decal "CacoScorch"
   States
   {
   Spawn:
      AFX1 ABC 1 Bright A_SpawnItemEx("DarkCyberImpPlasmaBallTrail",0,0,0,0,0,0,0,128)
      loop
   Death:
      TNT1 AAAAA 0 A_SpawnItemEx("DarkCyberImpPlasmaBallParticle",0,0,0,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(1,360))
	  TNT1 A 0 A_Explode(64,128)
      AFX1 DEFGHI 3 Bright 
      stop
   }
}

Actor DarkCyberImpPlasmaBallTrail : DarkCyberImpFireBallParticle
{
    Scale 0.9
    States
    {
    Spawn:
		AFX1 A 1 Bright A_FadeOut(0.04)
		AFX1 A 0 A_SetScale(ScaleX-0.03)
		AFX1 B 1 Bright A_FadeOut(0.04)
		AFX1 B 0 A_SetScale(ScaleX-0.03)
		AFX1 C 1 Bright A_FadeOut(0.04)
		AFX1 C 0 A_SetScale(ScaleX-0.03)
		Loop
    }
}

Actor DarkCyberImpPlasmaBallParticle
{
	Projectile
	Damage 0
	+NoBlockMap
	+NoInteraction
	+DoomBounce
	+ClientSideOnly
	+ThruActors
	Speed 0
	Radius 1
	Height 1
	RenderStyle Add
	States
	{
  Spawn:
    TNT1 AAAAA 1 Bright A_SpawnItemEx("DarkCyberImpPlasmaBallParticleTwo",0,0,0,frandom(-1,1),frandom(-1,1),frandom(-1,1),random(1,360), SXF_TRANSFERTRANSLATION)
    Stop
	}
}

Actor DarkCyberImpPlasmaBallParticleTwo : DarkCyberImpPlasmaBallParticle
{
    Scale 0.15
	States
	{
	Spawn:
    PLSE ABCDE 3 bright
	stop
	}
}

Actor DarkCyberImpNightParticle : BfgParticle
{
	Scale 1.0
	Alpha 1.0
	+Noclip
	Translation "0:255=%[0.13,0.00,0.13]:[2.00,0.58,2.00]"
	States
	{
	Spawn:
	Death:
    CFX3 A 1 Bright A_FadeOut(0.04)
	CFX3 A 0 A_SetScale(ScaleX*0.9)
	CFX3 A 1 Bright A_FadeOut(0.04)
	CFX3 A 0 A_SetScale(ScaleX*0.9)
	CFX3 A 1 Bright A_FadeOut(0.04)
	CFX3 A 0 A_SetScale(ScaleX*0.9)
	CFX3 B 1 Bright A_FadeOut(0.04)
	CFX3 B 0 A_SetScale(ScaleX*0.9)
	CFX3 B 1 Bright A_FadeOut(0.04)
	CFX3 B 0 A_SetScale(ScaleX*0.9)
	CFX3 B 1 Bright A_FadeOut(0.04)
	CFX3 B 0 A_SetScale(ScaleX*0.9)
    Loop
	}
}

Actor DarkCyberImpFiraCounter : Inventory
{
	inventory.maxamount 4
}

ACTOR CyberDarkImpArm : CyberArm
{
	Translation "48:79=[154,154,154]:[0,0,0]"
} 