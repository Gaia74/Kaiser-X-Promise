ACTOR Cryocubus 7585 
{ 
  //$Category Mancubus
  Health 600
  Radius 48
  Height 64
  Mass 1000
  Speed 8
  PainChance 80
  MaxTargetRange 300
  Monster
  +FLOORCLIP
  +DONTHARMSPECIES
  Species "KX" 
  DamageFactor "KX", 0.0
  SeeSound "Cryocubus/See"
  PainSound "Cryocubus/Pain"
  DeathSound "Cryocubus/Death"
  ActiveSound "Cryocubus/Pain"
  Obituary "%o was frozen by the Cryocubus."
  var int user_fire;
  BloodColor "00 D8 FF"
  States
  {
  Spawn:
    CRYM AB 15 A_Look
    Loop
  See:
    CRYM A 4 A_Chase
	TNT1 A 0 A_PlaySound("Mancubus/FootStep")
	CRYM ABBCCD 4 A_Chase
	TNT1 A 0 A_PlaySound("Mancubus/FootStep")
	CRYM DEEFF 4 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfCloser(304,"Gas")
    TNT1 A 0 A_Jump(256,"Comet","Spread","Big")
    Goto See
  Gas:
	TNT1 A 0 A_PlaySound("Cryocubus/Roar",CHAN_WEAPON)
	CRYM FG 6 A_FaceTarget
	TNT1 A 0 A_SetUserVar(user_fire,0)
	TNT1 A 0 A_PlaySound("Cryocubus/IceBreath",CHAN_VOICE)
	Goto LoopingGas
  LoopingGas:
	CRYM H 0 A_SetUserVar(user_fire,user_fire+1)
	CRYM H 0 bright A_CustomMissile("KXCryocubusGas",30,-25,frandom(-2,2)) 
	CRYM H 0 bright A_CustomMissile("KXCryocubusGas",30,25,frandom(-2,2))
	CRYM H 1 A_FaceTarget
	CRYM H 0 A_JumpIf(user_fire>30,"See")
	Loop
  Comet:
    TNT1 A 0 A_PlaySound("Cryocubus/Roar",CHAN_WEAPON)
    CRYM GGGG 5 A_FaceTarget
	CRYM H 0 Bright A_PlaySound("Cryocubus/Comet")
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,25,frandom(-15,15),2,random(5,20))
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,-25,frandom(-15,15),2,random(5,20))
	CRYM HHIG 5 A_FaceTarget
	CRYM H 0 Bright A_PlaySound("Cryocubus/Comet")
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,25,frandom(-15,15),2,random(5,20))
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,-25,frandom(-15,15),2,random(5,20))
	CRYM HHIG 5 A_FaceTarget
	CRYM H 0 Bright A_PlaySound("Cryocubus/Comet")
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,25,frandom(-15,15),2,random(5,20))
	CRYM H 0 Bright A_CustomMissile("KXCryocubusComet",30,-25,frandom(-15,15),2,random(5,20))
	CRYM HHIG 5 A_FaceTarget
	Goto See
  Spread:
    CRYM GG 5 A_FaceTarget
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,-32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,-32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,-32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,-32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM H 1 BRIGHT A_CustomMissile("ThrallShot1",30,-32,random(-5,5),CMF_OFFSETPITCH,frandom(-2,5))
	CRYM IG 5 A_FaceTarget
	Goto See
  Big:
    CRYM G 10 A_FaceTarget
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,25,0)
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,-25,0)
	CRYM HIG 5 A_FaceTarget
	TNT1 A 0 A_Jump(128,1)
	Goto See
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,25,0)
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,-25,0)
	CRYM HIG 5 A_FaceTarget
	TNT1 A 0 A_Jump(128,1)
	Goto See
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,25,0)
	CRYM H 0 Bright A_CustomMissile("CryocubusShot",30,-25,0)
	CRYM HIG 5 A_FaceTarget
	Goto See
  Pain:
    CRYM J 3
	CRYM J 3 A_Pain
    Goto See
  Death:
    CRYM J 6 A_SpawnitemEx("KXBloodExplosionEffect",0,0,32,0,0,0,0,SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION)
    CRYM L 0 A_Scream
	CRYM KKK 2 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(1,10), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
	CRYM M 0 A_NoBlocking
    CRYM LLLMMMMMNNNNNOOOPPP 2 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(1,10), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_USEBLOODCOLOR | SXF_NOCHECKPOSITION)
    CRYM P -1 A_BossDeath
    Stop
  Raise:
    CRYM R 5
    CRYM QPONMLK 5
    Goto See
  }
}

Actor KXCryocubusGas
{
  Radius 15
  Height 8
  Speed 20
  Scale 0.2
  Damage (2)
  RenderStyle Add
  Alpha 1.0
  Projectile
  DamageType Ice
  +HitTracer
  States
  {
  Spawn:
      CRYM Q 0 Bright A_SetScale(ScaleX+0.02)
      CRYM Q 1 Bright A_FadeOut(0.04)
      Loop
  Death:
	  TNT1 A 0 A_TakeInventory("KXCryocubusGasSlow",1,AAPTR_TRACER)
	  TNT1 A 0 A_GiveInventory("KXCryocubusGasSlow",1,AAPTR_TRACER)
      CRYM Q 0 Bright A_SetScale(ScaleX+0.02)
      CRYM Q 1 Bright A_FadeOut(0.04)
      Goto Death+2
  }
}

Actor KXCryocubusGasSlow : PowerSpeed
{
    Speed 0.9
	Powerup.Duration -1
	Powerup.Color "01 F8 FF", 0.1
}

Actor KXCryocubusFrost : PowerSpeed
{
    Speed 0.33
	Powerup.Duration -5
	Powerup.Color "01 F8 FF", 0.6
}

Actor KXCryocubusFrost2 : PowerSpeed
{
    Speed 0.5
	Powerup.Duration -3
	Powerup.Color "01 F8 FF", 0.3
}

Actor KXCryocubusComet
{
  Radius 13
  Height 8
  Speed 25
  Scale 0.75
  Gravity 0.75
  Damage 0
  Projectile
  -NOGRAVITY
  +DOOMBOUNCE
  SeeSound "Cryocubus/Comet"
  Translation "0:255=%[0.00,0.00,2.00]:[2.00,2.00,2.00]"
  RenderStyle Add
  DamageType Ice
  States
  {
  Spawn:
    COMT AAAABBBBCCCC 1 Bright A_SpawnItemEx("KXCryocubusCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
    Loop
  Death:
    COMT A 0 A_PlaySound("Cryocubus/CometDeath")
	COMT A 0 Bright A_RadiusGive("KXCryocubusFrost",144,RGF_PLAYERS,1)
    COMT A 0 Bright A_Explode(108,128)
	COMT AAAAAAAAAAAAAAA 0 A_SpawnItemEx("CultistParticle1",0,0,0,frandom(-10,10),frandom(-10,10),frandom(-10,10),random(1,360))
    COMT DDDDEEEEFFFFGGGGHHHHIIII 1 Bright A_SpawnItemEx("KXCryocubusCometDust",0,0,0,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(1,360))
    Stop
  }
}

Actor KXCryocubusCometTail
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
	Scale 0.75
	Translation "0:255=%[0.00,0.00,2.00]:[2.00,2.00,2.00]"
	RenderStyle Add
    PROJECTILE
    +DontSplash
    +ClientSideOnly
    +NoBlockMap
    +NoInteraction
    States
    {
    Spawn:
	    TNT1 A 2 
        FRTB ABCDEFGHI 1 Bright
		Stop
    }
}

Actor KXCryocubusCometDust
{
  Radius 1
  Height 1
  Speed 0
  Damage 0
  Scale 0.5
  Projectile
  +DontSplash
  +ClientSideOnly
  +NoBlockMap
  +NoInteraction
  +HexenBounce
  Translation "0:255=%[0.00,0.00,2.00]:[2.00,2.00,2.00]"
  States
  {
  Spawn: 
      FRME ABCDEFGHIJKLMNS 1 Bright
	  FRME S 1 A_FadeOut(0.04)
      Wait
  }
}

Actor CryocubusShot
{
  Radius 3
  Height 3
  Speed 25
  Damage 6
  Scale 0.75
  Projectile
  RenderStyle Add
  DamageType Ice
  SeeSound "Cryocubus/Ball"
  DeathSound "Cryocubus/BallDeath"
  +HitTracer
  States
  {
  Spawn:
    CMPR AAABBBCCC 1 Bright A_SpawnItemEx("CryocubusShotTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Loop
  Death:
    CMPR AAAAAAAAAA 0 A_SpawnItemEx("CultistParticle1",0,0,0,frandom(-10,10),frandom(-10,10),frandom(-10,10),random(1,360))
	TNT1 A 0 A_GiveInventory("KXCryocubusFrost2",1,AAPTR_TRACER)
	TNT1 A 0 A_SetScale(1.25)
	CMPR DEFGH 3 Bright 
    Stop
  }
}

Actor CryocubusShotTrail : KXDustParticle
{
	Scale 0.1
	Speed 0
	States
	{
  Spawn:
  Death:
    CRYM Q 0 Bright A_SetScale(ScaleX+0.01)
    CRYM Q 1 Bright A_FadeOut(0.05)
    Loop
	}
}