ACTOR Cyberhellknight : Baronofhell 7554 
{ 
  //$Category KaiserKnights
  obituary "%o was splayed by a Cyber Hell Knight"
  hitobituary "%o was ripped to shreds by a Cyber Hell Knight"
  health 800
  radius 24
  height 64
  mass 1000
  speed 12
  painchance 40
  meleerange 55
  seesound "cbaron/sight"
  painsound "cbaron/pain"
  deathsound "cbaron/death"
  activesound "cbaron/active"
  MONSTER
  -BOSSDEATH
  +MISSILEMORE
  +NORADIUSDMG
  +DONTHARMSPECIES
  Species "KX"
  DamageFactor "KX", 0.0
  DamageType "KX"
  Gravity 0.8
  states
  {
  Spawn:
    CBO2 AB 10 A_Look
    loop
  See:
    CBO2 A 3 A_Chase
    CBO2 A 3 A_Chase   
    CBO2 B 0 A_PlaySound("cbaron/metal",CHAN_6,1.0)
    CBO2 BB 3 A_Chase
    CBO2 CC 3 A_Chase    
	CBO2 D 0 A_PlaySound("cbaron/Step",CHAN_7,1.0)
	CBO2 D 3 A_Chase
    loop
  Melee:
    CBO2 PQ 4 A_FaceTarget
    CBO2 R 4 A_CustomComboAttack("BaronBall",32,5*random(6,20),"baron/melee")
    Goto See
  InstaMelee:
	CBO2 E 0 A_PlaySound("cbaron/DashHit",CHAN_ITEM,1.0)
    CBO2 E 0 A_FaceTarget
	CBO2 E 0 A_ScaleVelocity(0)
    CBO2 Q 4 A_CustomMeleeAttack(5*random(6,20),"cbaron/DashHit","","Melee",True)
	CBO2 RR 4 A_FaceTarget
    Goto See
  Missile:
    TNT1 A 0 A_Jump(256,"Baron","Rocket","Dash","Stomp")
	Goto See
  Rocket:
	CBO2 E 0 A_PlaySound("cbaron/Ready",CHAN_WEAPON,1.0)
    CBO2 EEEE 5 A_FaceTarget
	CBO2 F 0 Bright A_customMissile("KXCyberBaronRocketEffect",29,16,0)
    CBO2 F 5 Bright A_customMissile("CyberBaronRocket",27,16,0)
    CBO2 EEE 5 A_FaceTarget
    goto See
  Baron:
    CBO2 PQ 4 A_FaceTarget
    CBO2 R 2 Bright A_CustomComboAttack("BaronBall",32,5*random(6,20),"baron/melee")
	CBO2 R 2 Bright A_CustomMissile("BaronBall",32,0,frandom(-10,10))
    Goto See
  Stomp:
    CBO2 A 0 A_PlaySound("cbaron/roar",CHAN_VOICE,1.0)
    CBO2 PPPPPP 4 A_FaceTarget
	CBO2 P 0 Bright A_PlaySound("cbaron/jump")
	TNT1 A 0 ThrustThingZ(0,30,0,1)
	TNT1 A 0 A_Recoil(-30)
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("KXArachnobaronDust",0,0,3,random(-6,6),random(-6,6),random(0,1),random(0,360))
	TNT1 A 0 A_FaceTarget
	Goto StompAnimation
  StompAnimation:
	CBO2 P 1 BRIGHT A_SpawnItemEx("KXCBHKDashTrailOne",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
	CBO2 P 0 BRIGHT A_JumpIf(Momz==0,"EndStomp")
	Loop
  EndStomp:
    CBO2 Q 0 BRIGHT A_SpawnItemEx("KXStompEffect",0,0,3,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
	TNT1 A 0 A_PlaySound("cbaron/Stomp")
	TNT1 A 0 A_Explode(172,172)
	CBO2 R 20 A_ScaleVelocity(0)
	Goto See
  Dash:
    CBO2 P 5 A_FaceTarget
	CBO2 P 0 Bright A_PlaySound("cbaron/dash")
	TNT1 A 0 ThrustThingZ(0,8,0,1)
	TNT1 A 0 A_Recoil(-25)
	TNT1 A 0 A_FaceTarget
	Goto RealDash
  RealDash:
    CBO2 P 0 A_SpawnItemEx("KXCBHKDashTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 P 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 P 0 A_SpawnItemEx("KXCBHKDashTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 P 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 P 0 A_SpawnItemEx("KXCBHKDashTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 P 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 P 0 A_SpawnItemEx("KXCBHKDashTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 P 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 P 0 A_SpawnItemEx("KXCBHKDashTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 P 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 Q 0 A_SpawnItemEx("KXCBHKDashTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 Q 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 Q 0 A_SpawnItemEx("KXCBHKDashTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 Q 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 Q 0 A_SpawnItemEx("KXCBHKDashTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 Q 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 Q 0 A_SpawnItemEx("KXCBHKDashTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 Q 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 Q 0 A_SpawnItemEx("KXCBHKDashTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 Q 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 R 0 A_SpawnItemEx("KXCBHKDashTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 R 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 R 0 A_SpawnItemEx("KXCBHKDashTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 R 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 R 0 A_SpawnItemEx("KXCBHKDashTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 R 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 R 0 A_SpawnItemEx("KXCBHKDashTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 R 1 Bright A_JumpIfCloser(70,"DashCheck")
	CBO2 R 0 A_SpawnItemEx("KXCBHKDashTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	CBO2 R 1 Bright A_JumpIfCloser(70,"DashCheck")
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	TNT1 A 0 A_Jump(64,"Dash")
    Goto See
  DashCheck:
    TNT1 A 0 A_JumpIfTargetInsideMeleeRange("InstaMelee")
	Goto See
  Pain:
    CBO2 H 2
    CBO2 H 2 A_Pain
    goto See
  Death:
    CBO2 I 8
    CBO2 J 8 A_Scream
	CBO2 K 4 bright
	CBOS A 0 A_SpawnItemEx("KXCyberBaronExplosionDeathEffectSpawner",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION)
	CBO2 K 4 bright
    CBO2 L 8 bright A_NoBlocking
    CBO2 M 8 bright
    CBO2 N 8 bright
    CBO2 O -1
    stop
   }
}

ACTOR KXCBHKDashTrailOne
{   
    RenderStyle Translucent
	+NoGravity
    +ClientSideOnly
    +DontSplash
	+NoBlockMap
    +NoInteraction
	+Noclip
	Alpha 1.0
    States
    {
    Spawn:
      TNT1 A 1
	  CBO2 P 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}

ACTOR KXCBHKDashTrailTwo : KXCBHKDashTrailOne
{   
    States
    {
    Spawn:
      TNT1 A 1
	  CBO2 Q 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}

ACTOR KXCBHKDashTrailThree : KXCBHKDashTrailOne
{   
    States
    {
    Spawn:
      TNT1 A 1
	  CBO2 R 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}