ACTOR RailArachnotron 7576 
{ 
    //$Category KSpiders
	Health 500
	Radius 64
	Height 64
	Mass 600
	Speed 12
	PainChance 192
	Damage 30
	Monster
	+FLOORCLIP
	+DONTHARMSPECIES
    Species "KX"
	DamageFactor "KX", 0.0
	SeeSound "baby/sight"
	PainSound "baby/pain"
	DeathSound "baby/death"
	ActiveSound "baby/active"
	Obituary "%o was railed by an arachnotron."
	Decal RedRailScorch
	States
	{
	Spawn:
		RSPI AB 10 A_Look
		Loop
	See:
		RSPI A 20
		RSPI A 3 A_BabyMetal
		RSPI ABBCC 3 A_Chase
		RSPI D 3 A_BabyMetal
		RSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		RSPI A 0 A_PlaySound("KXRailgunSpider/Charge",CHAN_VOICE,1.0)
		RSPI G 0 A_FaceTarget
		RSPI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Bright A_SpawnItemEx("RailArachnotronChargeParticle",0,0,18,random(-3,3),random(-3,3),random(-3,3),random(0,360))
		RSPI A 0 A_PlaySound("KXRailgunSpider/Fire",CHAN_VOICE,1.0)
		RSPI H 4 Bright A_CustomRailgun (100,0,None,None,RGF_SILENT|RGF_FULLBRIGHT,1,1,"RailArachnotronPuff",0,0,8192,0,12.0,0.0,"RailArachnotronTrail",-14)
		RSPI G 8
		Goto See+1
	Pain:
		RSPI I 3
		RSPI I 3 A_Pain
		Goto See+1
	Death:
	    TNT1 A 0 A_SpawnItemEx("KXBloodExplosionEffect",0,0,36,0,0,0,random(1,360))
		TNT1 A 0 A_SpawnItemEx("KXArachnoExplosionDeath",0,0,36,0,0,0,random(1,360))
		RSPI J 20 A_Scream
		RSPI K 7 A_Fall
		RSPI LMNO 7
		RSPI P -1
		Stop
    Raise:
		RSPI P 5
		RSPI ONMLKJ 5
		Goto See+1
	}
}

Actor RailArachnotronChargeParticle
{
    +NoInteraction
	+NoBlockMap
	+ClientSideOnly
	Scale 0.1
    RenderStyle Add
	Translation "0:255=%[2.00,0.40,0.40]:[0.00,0.00,0.00]"
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(AAPTR_DEFAULT,VelX*16,VelY*16,VelZ*16,0,WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET|WARPF_ABSOLUTEANGLE)
        TNT1 A 0 A_ScaleVelocity(-1)
        BFS1 ABABABABABAB 1 BRIGHT
        BFS1 ABAB 1 BRIGHT A_FadeOut(0.2)
        Stop
    }
}

Actor RailArachnotronTrail
{
    Scale 1.0
    Radius 1
    Height 1
    Speed 0
    Damage 0
    PROJECTILE
    RenderStyle Add
	+ClientSideOnly
	+NoInteraction
	+NoBlockMap
	Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.31,0.31]"
    Alpha 1
    States
    {
    Spawn:
	    APLS A 0
		APLS A 0 A_SpawnItemEx("RailArachnotronTrailParticle",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4),random(1,360),SXF_TRANSFERTRANSLATION)
	    APLS A 0 Bright A_SetScale(ScaleX*0.95)
        APLS B 1 Bright A_FadeOut(0.04)
        Goto Spawn+2
    }
}

Actor RailArachnotronTrailParticle :  ArachnotronPlasmaParticle
{
    Alpha 1
	Scale 0.5
	States
	{
  Spawn:
  Death:
    APLS A 0 Bright A_SetScale(ScaleX*0.9)
    APLS A 1 Bright A_FadeOut(0.05)
    Loop
	}
}

ACTOR RailArachnotronPuff
{
  +NoBlockMap
  +NoGravity
  +AllowParticles
  +Randomize
  +PuffOnActors
  +AlwaysPuff
  Seesound "KXRailgunSpider/Explosion"
  translation "0:255=%[0.00,0.00,0.00]:[1.19,0.00,0.00]"
  Mass 5
  States
  {
  Spawn:
  Melee:
    TNT1 A 0
	TNT1 A 0 A_PlaySound("KXRailgunSpider/Explosion",CHAN_7,1.0)
    TNT1 AAAAAAAAAA 0 A_SpawnItemEx("RailArachnotronTrailParticle",0,0,0,frandom(-10,10),frandom(-10,10),frandom(-10,10),random(1,360),SXF_TRANSFERTRANSLATION)
	TFOG ABCDEFG 3 Bright 
	Stop
  }
}