ACTOR TechnoCaco 7558 
{ 
  //$Category AirForce
  OBITUARY "%o was injured by a metal grid defense system."
  HITOBITUARY "%o got too close to a metal grid defense system."
  Health 500
  Radius 31
  Height 56
  Mass 400
  Speed 10
  PainChance 30
  SeeSound "KXTechnoCaco/See"
  PainSound "KXTechnoCaco/Pain"
  ActiveSound "KXTechnoCaco/Act"
  MONSTER
  +NoBlood
  +Float
  +NoGravity
  +DontHarmSpecies
  Species "KX"
  DamageFactor "KX", 0.0
  states
  {
  Spawn:
    CABT A 10 A_Look
    Loop
  See:
    CABT A 3 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_Jump(256,"Silver", "Laser", "Rocket")
  Rocket:
    TNT1 A 0 A_PlaySound("Robot/Alert")
    CABT AABC 5 A_FaceTarget
    CABT D 5 BRIGHT A_CustomMissile("KXMetalBall",34,0,0,0)
    Goto See
  Silver:
	CABT BC 5 A_FaceTarget
	CABT DDDDD 1 BRIGHT A_CustomMissile("KXSilverBall",32,0,random(45,-45),CMF_OFFSETPITCH ,random(-5,5))
	TNT1 A 0 A_FaceTarget
	CABT DDDDD 1 BRIGHT A_CustomMissile("KXSilverBall",32,0,random(45,-45),CMF_OFFSETPITCH ,random(-5,5))
	TNT1 A 0 A_FaceTarget
	CABT DDDDD 1 BRIGHT A_CustomMissile("KXSilverBall",32,0,random(45,-45),CMF_OFFSETPITCH ,random(-5,5))
	TNT1 A 0 A_FaceTarget
	Goto See
  Laser:
    CABT B 5 A_FaceTarget
    CABT MNMNMN 1 BRIGHT A_CustomMissile("KXTCLaserBall",32,0,0,0)
    TNT1 A 0 A_Jump(80,"See")
    Goto Laser+1
  Pain:
    TNT1 AAA 0 A_SpawnitemEx("KXShrapnelMetalParticle",frandom(-3,3),frandom(-3,3),32,frandom(-5,5),frandom(-5,5),frandom(5,10),random(0,360),SXF_NOCHECKPOSITION)
    CABT E 3 A_Pain
    CABT F 3
    Goto See
  Death:
    TNT1 A 0 A_PlaySound("Rocketimp/Explode")
    TNT1 AAAAAAAAAA 0 A_SpawnitemEx("KXShrapnelMetalParticle",frandom(-5,5),frandom(-5,5),32,frandom(-5,5),frandom(-5,5),frandom(5,10),random(0,360),SXF_NOCHECKPOSITION)
	TNT1 AAAA 0 A_SpawnitemEx("KXSmallExplosionEffect1",frandom(-5,5),frandom(-5,5),frandom(10,40),0,0,0,0,SXF_NOCHECKPOSITION)
    CABT G 8
    CABT H 8
    CABT IJ 8
	Wait
  Crash:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnitemEx("KXShrapnelMetalParticle",frandom(-5,5),frandom(-5,5),5,frandom(-10,10),frandom(-10,10),frandom(7,15),random(0,360),SXF_NOCHECKPOSITION)
	TNT1 A 0 A_Scream
    CABT K 8 A_NoBlocking
    CABT L -1 A_SetFloorClip
    Stop
  }
}

ACTOR KXMetalBall
{
   Radius 12
   Height 12
   Speed 20
   Damage 10
   Alpha 1.0
   Projectile
   SeeSound "KXTechnoCaco/Shot1"
   DeathSound "KXTechnoCaco/Shot2"
   states
   {
   Spawn:
		THCA AAABBBCCC 1 BRIGHT A_SpawnItemEx("KXMetalBallTrail",0,0,0,0,0,0,0,128)
		Loop
   Death:
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 0,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 30,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 60,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 90,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 120,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 150,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, -30,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, -60,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, -90,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, -120,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, -150,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_CustomMissile("BorgNail1", 4, 2, 0,CMF_ABSOLUTEPITCH,2)
		TNT1 A 0 A_Explode(80,128)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("KXExplosiveFireBallParticle",0,0,36,frandom(-10,10),frandom(-10,10),frandom(5,10),random(1,360))
		TNT1 AAAAAAAAAA 0 A_SpawnitemEx("KXNailborgParticle", 0, 0, 0, frandom(-3,3), frandom(-3,3), frandom(5,10), random(0,360),SXF_NOCHECKPOSITION)
		THCA EFGHI 3 Bright
		Stop
   }
}

Actor KXMetalBallTrail
{
  Projectile
  RenderStyle Add
  +NoBlockMap
  +ClientSideOnly
  +Noclip
  Scale 0.75
  Alpha 1.0
  States
  {
  Spawn:
    TNT1 A 2
    FRTB ABCDEFGHI 1 Bright A_FadeOut(0.05)
    Stop
  }
}

ACTOR KXSilverBall
{
   Radius 12
   Height 12
   Speed 1
   Damage 1
   Alpha 1.00
   Projectile
   SeeSound "crack/see"
   DeathSound "Weapons/Nailhit"
   states
   {
   Spawn:
      MT02 A 0
      MT02 A 0 A_ScaleVelocity(frandom(10.0,20.0))
      MT02 ABC 3 BRIGHT 
	  Goto Spawn+2
   Death:
      TNT1 AAA 0 A_SpawnitemEx("KXNailborgParticle", 0, 0, 0, frandom(-3,3), frandom(-3,3), frandom(2,4), random(0,360),SXF_NOCHECKPOSITION)
	  MT02 DEFGH 3
      Stop
   }
}

ACTOR KXTCLaserBall : Gaia74ArachnotronPlasma
{
  Radius 10
  Height 6
  Speed 30
  Scale 0.6
  Damage (2)
  Projectile
  +RANDOMIZE
  RenderStyle Add 
  Alpha 0.75
  SeeSound "KXTechnoCaco/Shot3"
  DeathSound "baby/shotx"
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.22,0.22]"
  States
  {
  Spawn:
    APLS AB 1 Bright
    Loop
  Death:
    APBX ABCDE 3 Bright A_SpawnItemEx("ArachnotronPlasmaParticle",0,0,0,random(-3,3),random(-3,3),random(-3,3),random(1,360),SXF_TRANSFERTRANSLATION)
    Stop
  }
}