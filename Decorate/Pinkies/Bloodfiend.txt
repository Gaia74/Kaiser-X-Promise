Actor Bloodfiend : Demon 
{
  Health 300
  PainChance 140
  Speed 12
  Radius 30
  Height 56
  Mass 450
  Monster
  +FloorClip
  +LongMeleeRange
  +DontHurtSpecies
  Obituary "%o died from the toxic blood of a bloodfiend."
  HitObituary "%o was eaten by a bloodfiend."
  SeeSound "Monster/sg2sit"
  AttackSound "Monster/sg2atk"
  PainSound "demon/Pain"
  DeathSound "demon/Death"
  ActiveSound "demon/sg2act"
  States
  {
  Spawn:
    SAR2 AB 10 A_Look
    Loop
  See:
    SAR2 AA 2 A_Chase
    SAR2 A 0 A_JumpIfCloser(224, "Jump")
    SAR2 BB 2 A_Chase
    SAR2 A 0 A_JumpIfCloser(224, "Jump")
    SAR2 CC 2 A_Chase
    SAR2 A 0 A_JumpIfCloser(224, "Jump")
    SAR2 DD 2 A_Chase
    SAR2 A 0 A_JumpIfCloser(224, "Jump")
    Loop
  Melee:
    SAR2 EF 8 A_FaceTarget
    SAR2 G 8 A_SargAttack
    Goto See
  Missile:
    TNT1 A 0 A_Jump(256, "Blood", "Jump")
  Blood:
    SAR2 GF 8 A_FaceTarget
    SAR2 E 0 A_PlaySound("Weapons/bloodf")
    SAR2 E 0 A_CustomMissile("Bloodshot",22,0,-1,0,0)
    SAR2 E 8 A_CustomMissile("Bloodshot",22,0,1,0,0)
    Goto See
  Jump:
    SAR2 E 4 A_FaceTarget 
    SAR2 F 0 A_SkullAttack
    SAR2 F 10 A_SkullAttack 
    SAR2 G 5 A_Gravity
    Goto See
  Pain:
    SAR2 H 2
    SAR2 H 2 A_Pain
    Goto See
  Death:
    SAR2 I 8
    SAR2 J 8 A_Scream
    SAR2 K 4
    SAR2 L 4 A_NoBlocking
    SAR2 M 4
    SAR2 N -1
    Stop
  XDeath:
    SAR2 O 5
    SAR2 P 5 A_XScream
    SAR2 Q 5 A_NoBlocking
    SAR2 RSTUV 5
    SAR2 W -1
    Stop
  Raise:
    SAR2 NMLKJI 5
    Goto See
  }
}

Actor Bloodshot
{
  Radius 3
  Height 3
  Speed 20
  Damage 3
  Projectile
  RenderStyle Translucent
  Alpha 0.80
  +ThruGhost
  DeathSound "weapons/bloodx"
  States
  {
  Spawn:
    FBLD A 2 Bright A_CStaffMissileSlither
    FBLD A 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128)
    FBLD B 2 Bright A_CStaffMissileSlither
    FBLD B 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128)
    Loop
  Death:
    TNT1 A 0 A_SpawnItemEx("Blood", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("BloodSplatter", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("Blood", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("BloodSplatter", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("Blood", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("BloodSplatter", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("Blood", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    TNT1 A 0 A_SpawnItemEx("BloodSplatter", 1, 1, random(8,32), random(0,5), random(0,5), 0, random(0,360), 160, 0)
    FBLD CDE 3 Bright
    Stop
  }
}

Actor Bloodtrail
{
  Radius 1
  Height 1
  Projectile
  RenderStyle Translucent
  Alpha 0.67
  +CLIENTSIDEONLY
  States
  {
  Spawn:
    TNT1 A 3
    FBLD FGH 3 BRIGHT
    Stop
  }
}

Actor Lurker : Bloodfiend 
{
  RenderStyle Translucent
  Obituary "%o didn't See the lurker that was shooting at him."
  HitObituary "%o didn't See the lurker right in front of him."
  DropItem BloodAmmo
  Alpha 0.25
//SpawnID 171
}