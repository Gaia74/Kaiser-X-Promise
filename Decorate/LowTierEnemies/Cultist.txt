Actor KXCultist 7518
{ 
  //$Category LowTierSoldiers 
  Health 50
  GibHealth 20
  PainChance 100
  Scale 0.90
  Speed 15
  Radius 20 
  Height 56 
  Mass 100 
  Monster
  +MissileMore
  +FloorClip
  +DONTHARMSPECIES
  Species "KX"
  DamageFactor "KX", 0.0
  Obituary "%o was freezed by a cultist."
  Translation "168:191=192:207", "16:47=192:207"
  SeeSound "monster/culsit" 
  AttackSound "monster/culatk" 
  PainSound "monster/culpai" 
  DeathSound "monster/culdth" 
  ActiveSound "monster/culsit"
  States 
  { 
  Spawn: 
    CULT AB 10 A_Look 
    Loop 
  See: 
    CULT AABB 3 A_Chase 
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/Light")
	CULT CCDD 3 A_Chase 
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/Light")
    Loop 
  Missile:
    CULT EE 5 A_FaceTarget
    CULT F 0 Bright A_CustomMissile("ThrallShot1",42,-6,0,1,0)
    CULT F 0 Bright A_CustomMissile("ThrallShot1",42,6,0,1,0)
	CULT F 0 Bright A_CustomMissile("CultistParticle2",42,-6,0,1,0)
    CULT F 0 Bright A_CustomMissile("CultistParticle2",42,6,0,1,0)
    CULT F 8 Bright A_PlaySound("monster/culatk")
    Goto See
  Pain: 
    CULT G 2 
    CULT G 2 A_Pain 
    Goto See 
  Death: 
    TNT1 AAAAA 0 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_NOCHECKPOSITION)
    CULT H 8 
    CULT I 8 A_Scream 
    CULT J 4 
    CULT K 4 A_NoBlocking 
    CULT L 4 
    CULT M -1 
    Stop
  XDeath:
    CULT N 5 A_CustomMissile("KXBloodExplosionEffect",37,0,0,0,0)
    CULT O 5 A_XScream 
	CULT O 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_NOCHECKPOSITION)
    CULT P 0 A_NoBlocking 
    CULT PQRSTUV 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_NOCHECKPOSITION)
    CULT W -1 
  Raise: 
    CULT MLKJHI 5 
    Goto See 
  } 
}

Actor ThrallShot1
{
  Radius 4
  Height 6
  Speed 30
  Damage (random(7,15))
  RenderStyle Add
  Alpha 0.67
  Projectile
  DeathSound "monster/culatk2"
  SeeSound "monster/culatk"
  DamageType Ice
  States
  {
  Spawn:
    CFX4 AB 2 Bright A_SpawnItemEx("CultistParticle1",FRandom(-3,3),FRandom(-3,3),FRandom(-5,5),0,0,0,random(-180,180))
    Loop
  Death:
    TNT1 AAA 0 Bright A_SpawnItemEx("CultistParticle1",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4),random(-180,180))
    CFX4 CDEF 3 Bright
    Stop
  }
}

Actor CultistParticle1
{
	Projectile
	Damage 0
	+NOBLOCKMAP
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY 
	+NoBlockMap
    -BLOODSPLATTER
    +DontSplash
    +FORCEXYBILLBOARD
	Speed 0
	Radius 1
	Height 1
	Scale 0.5
	Alpha 0.67
	RenderStyle Add
	States
	{
  Spawn:
  Death:
    CFX4 CDEF 3 Bright
    Stop
	}
}

Actor CultistParticle2 : CultistParticle1
{
	Alpha 1.0
	Scale 1.0
	States
	{
  Spawn:
  Death:
    TNT1 AAA 0 Bright A_SpawnItemEx("CultistParticle1",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4),random(-180,180))
    CFX4 CDEF 2 Bright
    Stop
	}
}

Actor KXCultistFire : KXCultist 5518
{ 
  Translation "192:207=168:191"
  var int user_fire;
  States 
  { 
  Missile:
	CULT EEEEEEEEEE 1 {
		A_FaceTarget;
		A_SpawnItemEx("KXExplosiveFireBallParticle2", Random(24, 32), 0, frandom(4,40), 0, 0, (Random(20, 70)*0.1), Random(0, 360), 128);
    }
	CULT F 0 Bright A_CustomMissile ("KXCultistFireProjectile",42,6,frandom(-4,4),2,frandom(6,12))
    CULT F 0 Bright A_CustomMissile ("KXCultistFireProjectile",42,-6,frandom(-4,4),2,frandom(6,12))
    CULT F 8 Bright A_PlaySound("monster/culatk")
    Goto See
  } 
}

actor KXCultistFireProjectile : DoomImpBall
{
  RenderStyle Add
  Speed 15
  DontHurtShooter
  +RANDOMIZE
  -NOGRAVITY
  Gravity 0.5
  Scale 0.5
  Damage (0)
  states
  {
  Spawn:
    BAL1 AB 1 bright A_SpawnItemEx("KXCultistFireProjectileTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("CyberImpBallParticle",0,FRandom(-4,4),FRandom(-6,6),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    loop
  Death:
    TNT1 A 0 A_Explode(15,64)
	TNT1 AAAAAAA 0 A_SpawnItemEx("CyberImpBallParticle",0,0,0,frandom(-7,7),frandom(-7,7),frandom(-7,7),random(-180,180))
    BAL1 CDE 4 bright
    stop
  }
}

Actor KXCultistFireProjectileTrail : KXRocketTrail
{
	Scale 0.05
	Alpha 1.0
    States
    {
    Spawn:
	    TNT1 A 2 
		KXEX LMNOPQRSTUVWXY 1 Bright
	    KXEX Z 1 Bright A_FadeOut(0.1)
        Wait
    }
}

Actor KXCultistThunder : KXCultist 5519
{ 
  Translation "16:47=%[1.32,1.32,0.00]:[0.52,0.52,0.00]", "168:191=%[0.80,0.80,0.00]:[0.28,0.28,0.00]", "192:207=[255,255,0]:[0,0,0]"
  States 
  { 
  Missile:
    CULT EE 5 A_FaceTarget
    CULT F 0 Bright A_CustomMissile("KXCultistThunderProjectile",42,-6,0,1,0)
    CULT F 0 Bright A_CustomMissile("KXCultistThunderProjectile",42,6,0,1,0)
    CULT F 8 Bright A_PlaySound("monster/culatk4")
    Goto See
  } 
}

ACTOR KXCultistThunderProjectile : FastProjectile
{
   Radius 3
   Height 7
   Speed 70
   Damage (5)
   DamageType lightning
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.75
   +FORCEXYBILLBOARD
   Scale 0.5
   DeathSound "monster/culatk3"
   Translation "192:207=160:167"
   Decal "CacoScorch"
   States
   {
   Spawn:
      AFX1 ABC 1 Bright A_SpawnItemEx("KXCultistThunderProjectileTrail",0,0,0,0,0,0,0,128)
      loop
   Death:
      TNT1 AAAAA 0 A_SpawnItemEx("KXBlondeBoltParticle",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4),random(1,360))
      AFX1 DEFGHI 3 Bright 
      stop
   }
}

Actor KXCultistThunderProjectileTrail
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    RenderStyle Add
	Translation "192:207=160:167"
	Projectile
	+ClientSideOnly
    +Randomize
    Alpha 0.9
	Scale 0.5
    States
    {
    Spawn:
	    TNT1 A 0
		TNT1 A 1
		Goto Looping
	Looping:
		AFX1 A 1 Bright A_FadeOut(0.04)
		AFX1 A 0 A_SetScale(ScaleX-0.04)
		AFX1 B 1 Bright A_FadeOut(0.04)
		AFX1 B 0 A_SetScale(ScaleX-0.04)
		AFX1 C 1 Bright A_FadeOut(0.04)
		AFX1 C 0 A_SetScale(ScaleX-0.04)
		Loop
    }
}