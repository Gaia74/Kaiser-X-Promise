ACTOR KaiserRemoteSentry  7523
{
   //$Category LowTierSoldiers 
   Radius 20
   Height 64
   Mass 10000
   Health 300 
   Speed 0
   MinMissileChance 10
   MONSTER
   +LOOKALLAROUND
   +NOBLOOD
   +SHOOTABLE
   +DONTHARMSPECIES
   +MISSILEMORE
   +MISSILEEVENMORE
   Species "KX"
   DamageFactor "KX", 0.0
   SeeSound "Robot/Alert"
   DeathSound "KaiserTurret/Die"
   Obituary "%o was fulled with bullets by a Kaiser turret"
   States
   {
   Spawn:
	   TURR A 5 A_Look
	   Loop
   See:
	   TURR A 5 A_Chase
	   Goto See
   Missile:
       TURR A 0 A_PlaySound("Robot/Alert")
	   TURR A 15 A_FaceTarget
	   TURR A 2 A_FaceTarget
	   TURR B 0 A_CustomMissile("PisCaseSpawn",43,0,0)
	   TURR B 0 A_PlaySound("KaiserTurret/Fire")
	   TURR B 0 Bright A_CustomMissile("KXTurretDustParticle",43,0,0)
	   TURR B 2 Bright A_CustomMissile("KXTurretBullet", 43,0,frandom(-11.5,11.5),CMF_OFFSETPITCH,frandom(-1,1))
	   TURR A 2 A_FaceTarget
	   TURR B 0 A_CustomMissile("PisCaseSpawn",43,0,0)
	   TURR A 0 A_PlaySound("KaiserTurret/Fire")
	   TURR B 0 Bright A_CustomMissile("KXTurretDustParticle",43,0,0)
	   TURR B 2 Bright A_CustomMissile("KXTurretBullet", 43,0,frandom(-11.5,11.5),CMF_OFFSETPITCH,frandom(-1,1))
	   TURR A 0 A_SpidRefire
	   Goto Missile+2
   Pain:
	   TURR A 5
	   Goto See
   Death:
       TURR C 0 A_PlaySound("weapons/rocklx", CHAN_Item)
       TURR C 0 A_Scream
       TURR C 0 A_SpawnItem ("KXRocketExplosionEffect", 0, 48)
	   TNT1 AAAAAAAAAAAAAAAAA 0 A_SpawnitemEx("KXShrapnelMetalParticle",frandom(-3,3),frandom(-3,3),random(15,35),frandom(-8,8),frandom(-8,8),frandom(5,15),random(0,360),SXF_NOCHECKPOSITION)
       TURR C 10
	   TNT1 A 0 Bright A_PlaySound("Robot/DeathEffect", CHAN_Weapon)
	   TURR C -1
       Stop
   }
}

ACTOR KXTurretBullet : KaiserXBullet
{
	Damage (1*random(1,4))
	Speed 60
}

Actor KXTurretDustParticle : KXDustParticle
{
	Scale 0.05
	Speed 2
	States
	{
  Spawn:
  Death:
    KDUS A 0 Bright A_SetScale(ScaleX+0.01)
    KDUS A 1 Bright A_FadeOut(0.05)
    Loop
	}
}