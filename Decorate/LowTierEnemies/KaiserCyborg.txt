ACTOR KaiserCyborg 7517
{
  //$Category LowTierSoldiers 
  obituary "%o was pulverized by Kaiser's Cyborgs."
  health 130
  radius 20
  height 56
  mass 100
  speed 10
  painchance 170
  seesound "KaiserCyborg/See"
  painsound "KaiserCyborg/Pain"
  deathsound "KaiserCyborg/Death"
  activesound "KaiserCyborg/Act"
  MONSTER
  Species "KX"
  DamageFactor "KX", 0.0
  +DONTHARMSPECIES
  +MISSILEMORE
  +FLOORCLIP
  var int user_fire;
  states
  {
  Spawn:
    DEVZ AB 10 A_Look
    loop
  See:
    DEVZ AA 3 A_Chase
	DEVZ BB 3 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/Light")
	DEVZ CC 3 A_Chase
	DEVZ DD 3 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/Light")
    loop
  Missile:
    TNT1 A 0 A_Jump(256,"Lighte", "Barrage")
  Lighte:
    DEVZ E 8 Bright A_FaceTarget
    DEVZ F 3 Bright A_CustomMissile("KaiserCyborgPlasmaBarrage",28,9,frandom(-1,1),CMF_OFFSETPITCH,frandom(-1,1))
    DEVZ E 6 Bright A_FaceTarget
	DEVZ F 3 Bright A_CustomMissile("KaiserCyborgPlasmaBarrage",28,9,frandom(-1,1),CMF_OFFSETPITCH,frandom(-1,1))
    DEVZ E 6 Bright A_FaceTarget
	DEVZ F 3 Bright A_CustomMissile("KaiserCyborgPlasmaBarrage",28,9,frandom(-1,1),CMF_OFFSETPITCH,frandom(-1,1))
    DEVZ E 6 Bright A_FaceTarget
	Goto See
  Barrage:
    DEVZ E 0 A_PlaySound("KaiserCyborg/Blaster",CHAN_AUTO)
    DEVZ EEEEEE 5 Bright A_FaceTarget
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_SetUserVar(user_fire,random(7,15))
    DEVZ U 3 Bright A_CustomMissile("KaiserCyborgPlasmaBarrage",28,9,frandom(-3,3),CMF_OFFSETPITCH,frandom(-3,3))
    DEVZ E 3 Bright A_FaceTarget
	TNT1 A 0 A_SetUserVar(user_fire,user_fire-1)
	TNT1 A 0 A_JumpIf(user_fire==0, "See")
    Goto Barrage+9
  Pain:
    DEVZ G 3
    DEVZ G 3 A_Pain
    goto See
  Death:
    DEVZ H 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ I 0 A_Scream
	DEVZ I 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ J 0 A_NoBlocking
	DEVZ J 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ KLM 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ N -1
    stop
  XDeath:
    TNT1 A 0 A_SpawnitemEx("KXBloodExplosionEffect",0,0,32,0,0,0,0,SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ O 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ P 0 A_XScream
	DEVZ P 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ Q 0 A_NoBlocking
	DEVZ Q 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ RS 5 A_SpawnitemEx("GaiaBloodProjectile", frandom(-3,3), frandom(-3,3), random(5,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    DEVZ T -1
    stop
  Raise:
    DEVZ NMLKJIH 5
    goto See
  }
}

Actor KaiserCyborgPlasmaBarrage
{
  Radius 13
  Height 8
  Speed 30
  Damage (random(4,10)) 
  Scale 0.4 
  RenderStyle Add
  Translation "0:255=%[2.00,2.00,0.00]:[2.00,2.00,2.00]"
  SeeSound "KXFusionSpider/Shot" 
  DeathSound "KXFusionSpider/ShotDeath"
  Projectile
  Decal PlasmaScorchLower
  states
  {
  Spawn:
    FS03 AB 3 Bright A_SpawnItemEx("KaiserCyborgPlasmaBarrageParticle",FRandom(-3,3),FRandom(-3,3),FRandom(-4,4),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    Loop
  Death:
    TNT1 AAA 0 A_SpawnItemEx("KaiserCyborgPlasmaBarrageParticle",0,0,4,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(-180,180))
    FS03 HIJG 3 Bright
    Stop
  }
}

Actor KaiserCyborgPlasmaBarrageParticle
{
	Projectile
	Damage 0
	+NoBlockMap
	+BloodlessImpact
	+ClientSideOnly
    -BloodSplatter
    +DontSplash
    +ForceXYBillBoard
	Speed 0
	Radius 1
	Height 1
	Scale 0.5
	Alpha 1.0
	RenderStyle Add
	Translation "0:255=%[2.00,2.00,0.00]:[2.00,2.00,2.00]"
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(ScaleX*0.9)
		FS03 F 1 Bright A_FadeOut(0.05)
		Loop
	}
}
