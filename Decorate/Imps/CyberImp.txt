ACTOR CyberImp 7529 { 
//$Category Imps
//Game Doom
  Health 100
  Radius 20
  Height 56
  Mass 100
  Speed 12
  PainChance 150
  Monster
  +FLOORCLIP
  +MISSILEMORE
  +DONTHARMSPECIES
  Species "KX"
  DamageFactor "KX", 0.0
  SeeSound "CIMPSIT"
  PainSound "CIMPPAIN"
  DeathSound "CIMPDTH"
  ActiveSound "CIMPACT"
  HitObituary "%o was torn apart by a cyber imp"
  Obituary "%o was fried by a cyber imp"
  MeleeRange 66
  GibHealth 50
  States
  {
  Spawn:
    CIMP AB 10 A_Look
    Loop
  See:
	CIMP AA 2 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/NoShoe",CHAN_7)
	CIMP BB 2 A_Chase
	CIMP CC 2 A_Chase
	TNT1 A 0 A_PlaySound("KXMonsterFootstep/NoShoe",CHAN_7)
	CIMP DD 2 A_Chase
    Loop
  Melee:
    CIMP EF 4 A_FaceTarget
	TNT1 A 0 A_JumpIfCloser(56,1)
    CIMP F 0 A_Custommeleeattack(random(10, 20), "imp/melee") 
	CIMP FFFF 1 A_Recoil(-1)
    Goto See
  InstaMelee:
	CIMP E 0 A_PlaySound("imp/melee")
    CIMP E 0 A_FaceTarget
	CIMP E 0 A_ScaleVelocity(0)
    CIMP F 4 A_CustomMeleeAttack(random(20,30),"imp/melee","","Melee",True)
	CIMP GG 4 A_FaceTarget
    Goto See
  Missile: 
	TNT1 E 0 A_JumpIfCloser(302,"Close")
    CIMP E 0 A_FaceTarget
    CIMP EEEEEEEEEEEE 1 A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,18,25,frandom(-1,1),frandom(-1,1),frandom(-1,1))
	CIMP F 3 A_FaceTarget
	CIMP G 3 A_Custommissile("CyberImpPlasmaBall",36,4,frandom(-1,1),CMF_OFFSETPITCH,frandom(-1,1)) 
    Goto See
  Close:
    TNT1 A 0 A_Jump(256,"Imp","Jump")
  Imp:
    KCIM AB 6 A_FaceTarget
    KCIM C 6 A_CustomMissile ("CyberImpBall",32,12,frandom(-2,2),2,frandom(9,12))
	TNT1 A 0 A_Jump(128,1)
	Goto See
	KCIM AB 6 A_FaceTarget
    KCIM C 6 A_CustomMissile ("CyberImpBall",32,12,frandom(-2,2),2,frandom(9,12))
	Goto See
  Jump:
	KCIM A 4 A_FaceTarget
	TNT1 A 0 Bright A_PlaySound("CIMPJUMP")
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	TNT1 A 0 ThrustThingZ(0,30,0,1)
	TNT1 A 0 A_Recoil(-20)
	TNT1 A 0 A_FaceTarget
	Goto Jumping
  Jumping:
	KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
	KCIM A 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM A 0 A_SpawnItemEx("CyberImpJumpTrailOne", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM B 0 A_SpawnItemEx("CyberImpJumpTrailTwo", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM B 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
    KCIM C 0 A_SpawnItemEx("CyberImpJumpTrailThree", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
    KCIM C 1 Bright A_JumpIfCloser(70,"EndJumping")
	TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	Goto See
	EndJumping:
	TNT1 A 0 A_JumpIfTargetInsideMeleeRange("InstaMelee")
	Goto See
  Pain:
    CIMP H 2
    CIMP H 2 A_Pain
    Goto See
  Death:
	CIMP AAAAAAAAAA 0 A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(0,2), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
	CIMP I 7
    CIMP J 7 A_Scream
    CIMP K 7
    CIMP L 7 A_NoBlocking
    CIMP M -1
    Stop
  XDeath:
	CIMP N 0 A_CustomMissile("KXBloodExplosionEffect",37,0,0,0,0)
    CIMP N 0 A_CustomMissile("cyberArm",32,-10,270)
	CIMP NNNNN 1 A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(10,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    CIMP O 0 A_XScream
	CIMP OOOOOPPPPP 1  A_SpawnitemEx("GaiaBloodProjectile", frandom(-5,5), frandom(-5,5), frandom(10,42), frandom(-6,6), frandom(-6,6), frandom(2,12), random(0,360), SXF_TRANSFERTRANSLATION | SXF_NOCHECKPOSITION)
    CIMP Q 5 A_NoBlocking
    CIMP RST 5
    CIMP U -1
    Stop
  Raise:
    CIMP MLKJI 8
    Goto See
  }
}

actor CyberImpPlasmaBall : FastProjectile
{
  Radius 8
  Height 8
  Speed 40
  Damage (Random(15,25))
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  Scale 0.25
  MissileType CyberImpPlasmaBallTrail
  MissileHeight 8
  SeeSound "weapons/plasmaf"
  DeathSound "fuzzy/pdeath"
  States
  {
  Spawn:
    PLSS AB 1 bright
    loop
  Death:
    PLSE AAAAA 0 bright A_SpawnItemEx("CyberImpPlasmaExplosionEffect",0,0,0,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(1,360))
    TNT1 A 0 A_SetScale(0.5)
	PLSE BCDEF 2 bright
	PLSE GHIJKLMN 1 bright
	stop
  }
}

Actor CyberImpPlasmaBallTrail
{
    Scale 0.25
    Radius 1
    Height 1
    Speed 0
    Damage 0
    PROJECTILE
    RenderStyle Add
	+NOINTERACTION
    +DONTSPLASH
	+CLIENTSIDEONLY
    Alpha 1.0
    States
    {
    Spawn:
		PLSS A 1 bright A_FadeOut(0.25)
		PLSS A 0 A_SetScale(ScaleX*0.95)
		PLSS B 1 bright A_FadeOut(0.25)
		PLSS A 0 A_SetScale(ScaleX*0.95)
		Loop
    }
}

Actor CyberImpPlasmaExplosionEffect
{
	Projectile
	Damage 0
	+NOBLOCKMAP
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY 
	+NoBlockMap
    -BLOODSPLATTER
    +DontSplash
    +FORCEXYBILLBOARD
	Speed 0
	Radius 1
	Height 1
	RenderStyle Add
	States
	{
  Spawn:
    TNT1 AAAAA 2 Bright A_SpawnItemEx("CyberImpPlasmaExplosionEffectTwo",0,0,0,random(-1,1),random(-1,1),random(-1,1),random(1,360), SXF_TRANSFERTRANSLATION)
    Stop
	}
}

Actor CyberImpPlasmaExplosionEffectTwo : CyberImpPlasmaExplosionEffect
{
    Scale 0.1
	States
	{
	Spawn:
    PLSE BCDEF 2 bright
	PLSE GHIJKLMN 1 bright
	stop
	}
}

actor CyberImpBall : DoomImpBall
{
  RenderStyle Add
  Speed 25
  DontHurtShooter
  +RANDOMIZE
  -NOGRAVITY
  Gravity 0.4
  states
  {
  Spawn:
    BAL1 AB 1 bright A_SpawnItemEx("CyberImpBallTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("CyberImpBallParticle",0,FRandom(-4,4),FRandom(-6,6),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    loop
  Death:
    TNT1 A 0 A_Explode(10,64)
	TNT1 AAAAAAA 0 A_SpawnItemEx("CyberImpBallParticle",0,0,0,frandom(-7,7),frandom(-7,7),frandom(-7,7),random(-180,180))
    BAL1 CDE 4 bright
    stop
  }
}

Actor CyberImpBallParticle : KXExplosiveFireBallParticle3
{
    Scale 0.1
}

Actor CyberImpBallTrail : KXRocketTrail
{
	Scale 0.1
	Alpha 1.0
    States
    {
    Spawn:
	    TNT1 A 2 
		KXEX LMNOPQRSTUVWXY 1 Bright
	    KXEX Z 1 Bright A_FadeOut(0.1)
        Wait
    }
}

ACTOR CyberImpJumpTrailOne
{   
    RenderStyle Translucent
	+NoGravity
    +ClientSideOnly
    +DontSplash
	+NoBlockMap
    +NoInteraction
	+Noclip
	Alpha 1.0
	Translation "16:47=[255,255,255]:[0,0,0]", "168:191=[255,255,255]:[0,0,0]", "208:235=[255,255,255]:[0,0,0]"
    States
    {
    Spawn:
      TNT1 A 1
	  KCIM A 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}

ACTOR CyberImpJumpTrailTwo : CyberImpJumpTrailOne
{   
    States
    {
    Spawn:
      TNT1 A 1
	  KCIM B 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}

ACTOR CyberImpJumpTrailThree : CyberImpJumpTrailOne
{   
    States
    {
    Spawn:
      TNT1 A 1
	  KCIM C 1 A_FadeOut(0.05)
      Goto Spawn+1 
	  }
}

ACTOR KaiserMedic 7602 
{ 
//$Category BattleMedics
//Game Doom
  Health 500
  Radius 20
  Height 56
  Mass 200
  Speed 15
  Translation "64:79=96:111"
  PainChance 3
  Monster
  +FLOORCLIP
  SeeSound "CIMPSIT"
  PainSound "CIMPPAIN"
  DeathSound "CIMPDTH"
  ActiveSound "CIMPACT"
  HitObituary "%o was torn apart by a kaiser medic"
  Obituary "%o was fried by a kaiser medic"
  States
  {
  Spawn:
    CIMP AB 10 A_Look
    Loop
  See:
    CIMP AABB 2 A_VileChase
    CIMP CCDD 2 A_VileChase
    Loop
  Heal:
    CIMP EF 8 A_FaceTarget
    CIMP G 6 
    Goto See
  Melee:
    CIMP EF 8 A_FaceTarget
    CIMP F 6 A_Custommeleeattack(3 * random(5, 20), "imp/melee") 
    Goto See
  Missile:
    TNT1 A 0 A_Jump(128,"Plasma")
    TNT1 A 0 A_Jump(128,"Cure")
    Goto See
  Plasma:
    CIMP EF 8 A_FaceTarget
    CIMP G 6 A_Custommissile("plasmaBall",36,4)  
    CIMP GF 8 A_FaceTarget
    CIMP E 6 A_Custommissile("DoomImpBall",36,4)
    Goto See
  Cure:
    CIMP EF 8 A_FaceTarget
    CIMP G 0 Bright A_RadiusGive("Health", 96, RGF_MONSTERS, 50)
    CIMP G 8
    CIMP G 0 Bright A_RadiusGive("Health", 96, RGF_MONSTERS, 50)
    CIMP G 8
    Goto See
  Pain:
    CIMP H 2
    CIMP H 2 A_Pain
    Goto See
  Death:
    CIMP I 8
    CIMP J 8 A_Scream
    CIMP K 6
    CIMP L 6 A_NoBlocking
    CIMP M -1
    Stop
  XDeath:
    CIMP N 5 A_CustomMissile("cyberArm",72,-10,270)
    CIMP O 5 A_XScream
    CIMP P 5
    CIMP Q 5 A_NoBlocking
    CIMP RST 5
    CIMP U -1
    Stop
  }
}


ACTOR CyberArm
{
	Radius 8
	Height 8
	Speed 4
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
		CARM ABCDE 3
		Loop
	Death:
		CARM F -1
		Loop
	}
} 