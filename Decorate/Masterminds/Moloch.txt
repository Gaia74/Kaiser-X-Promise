//Moloch is a vicious cybernetic monster.  It has a devastating array of attacks: a lightning attack, a grenade attack, a nailgun, and a wraith-spitting attack.
//This monster is able to chain its attacks together in order to produce devastating combos. It will always chain each attack together randomly, so you may never know what attack may come next in the chain.
ACTOR Moloch 6665
{
	health 5000
	radius 50
	height 125
	mass 8000
	speed 20
	scale 1.1
	painchance 15
	damage 20
	Damagefactor "Moloch", 0
	Obituary "%o was sacrificed to Moloch."
	HitObituary "%o got shredded by Moloch's claws."
	seesound "emperor/attack1"
    painsound "emperor/pain"
    deathsound "emperor/death"
    activesound "emperor/attack1"
	bloodcolor orange
	MONSTER
	+BOSS
	+QUICKTORETALIATE
	+FLOORCLIP
	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTMORPH
	+NOICEDEATH
	+NOTARGET
	+TELESTOMP
	+DONTHARMSPECIES
    Species "KX"
	states
	{
	Spawn:
		CYEM AB 10 A_Look
		Loop
	See:
		CYEM AA 4 A_Chase
		CYEM B 0 A_PlaySound("moloch/step")
		CYEM BB 4 A_Chase
		CYEM CC 4 A_Chase
		CYEM D 0 A_PlaySound("moloch/step")
		CYEM DD 4 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Jump (256,"Missile1","Missile2","Missile3","Missile4")
	Missile1:
		CYEM H 0 Bright A_PlaySound("moloch/attack")
		CYEM HH 14 A_FaceTarget 
		CYEM I 10 A_CustomMissile ("MolochThunderbolt",65,0,0,0)
		CYEM I 0 A_Jump (70,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Missile2:
		TNT1 A 0 A_CheckSight ("See")
		CYEM F 0 Bright A_PlaySound("moloch/attack")
		CYEM F 12 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM E 6 A_CustomMissile("HellGrenade", 60,0,random(-10,10),2,random(-5,20))
		CYEM F 2 A_FaceTarget
		CYEM F 0 A_Jump (70,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Missile3:
		CYEM F 0 A_PlaySound ("moloch/attack")
		CYEM F 25 A_FaceTarget
		CYEM E 1 A_FaceTarget
		CYEM G 1 A_CustomMissile ("MolochNail", 55,random(-10,10),random(-1,1),0)
		CYEM G 0 A_PlaySound("moloch/nail")
		CYEM E 1 A_FaceTarget
		CYEM G 1 A_CustomMissile ("MolochNail", 55,random(-10,10),random(-1,1),0)
		CYEM G 0 A_PlaySound("moloch/nail")
		CYEM G 0 A_Jump (10, "Missile1","Missile2","Missile3","Missile4","See")
		CYEM G 1 A_SpidRefire
		Goto Missile3+5
	Missile4:
		CYEM H 0 Bright A_PlaySound("moloch/attack")
		CYEM HH 14 A_FaceTarget
		TNT1 A 0 A_CheckSight ("See")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_PainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_PainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_PainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_PainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_DualPainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_DualPainAttack("MolochWraith")
		CYEM I 5 A_FaceTarget
		CYEM I 0 A_Jump (70,"Missile1","Missile2","Missile3","Missile4","See")
		Goto See
	Pain:
		CYEM H 0 A_Pain
		CYEM H 0 Radius_Quake(15,15,0,40,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,30,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,60,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,90,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,120,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,150,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,180,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,210,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,240,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,270,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,300,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,330,0)
		CYEM H 0 A_CustomMissile("MolochScorch",0,0,0,0)
		CYEM H 10
		goto See
	Death:
	    CYEM I 20 A_Scream
		CYEM J 14 A_Noblocking
		CYEM K 14 A_KillChildren
		CYEM L 14
		CYEM MNONMNONMNO 7
		CYEM P 6
		CYEM Q 6
		CYEM Q 0 A_CustomMissile ("MolochDeathFire",0,0,0,0)
		CYEM Q 0 Radius_Quake(40,60,0,40,0)
		CYEM Q 0 A_PlaySound ("Moloch/thud")
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 0)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 10)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 20)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 30)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 40)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 50)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 60)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 70)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 80)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 90)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 100)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 110)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 120)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 130)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 140)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 150)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 160)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 180)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 190)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 200)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 210)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 220)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 230)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 240)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 250)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 260)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 270)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 280)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 290)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 300)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 310)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 320)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 330)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 340)
		CYEM Q 0 A_CustomMissile("MolochQuake", 0, -48, 350)
		CYEM R -1 A_BossDeath
		stop
	}
}

//Moloch's grenade attack.  These grenades spawn explosive embers upon death.
ACTOR HellGrenade
{
   Radius 10
   Height 10
   Speed 35
   Damage 20
   RENDERSTYLE Normal
   Damagetype "Fire"
   Scale 1.2
   PROJECTILE
   -NOGRAVITY
   +DOOMBOUNCE
   DONTHURTSHOOTER
   Seesound "moloch/grenade"
   DeathSound "moloch/grenexp"
   ExplosionDamage 35
   States
   {
   Spawn:
      MGRN AB 3 Bright A_SpawnItemEX("HellSmoke",0,0,0,0,0,0,0,2)
      loop
   Death:
      XPL1 A 0 Bright A_SetTranslucent (0.75,1)
      XPL1 A 2 Bright A_Explode
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(1,90),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(1,90),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(91,180),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(91,180),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(181,270),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(181,270),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(271,359),2,random(1,75))
      XPL1 A 0 Bright A_CustomMissile("HellGrenadeEmber",random(3,10),0,random(271,359),2,random(1,75))
      XPL1 BCDEF 2 Bright
      stop
	}
}

ACTOR HellGrenadeEmber
{
   Radius 5
   Height 5
   Speed 15
   Damage 5
   PROJECTILE
   RENDERSTYLE Add
   Alpha 0.90
   Decal DoomImpScorch
   Damagetype "Fire"
   DONTHURTSHOOTER
   +THRUGHOST
   -NOGRAVITY
   DeathSound "moloch/emberexp"
   ExplosionDamage 5
   States
   {
   Spawn:
      BAL3 AB 2 Bright A_SpawnItemEX("RedSmoke",0,0,0,0,0,0,0,2)
      loop
   Death:
      BAL3 CDE 4 Bright A_Explode
      stop
   }
}

ACTOR HellSmoke
{   
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   +CLIENTSIDEONLY
   RENDERSTYLE Translucent
   ALPHA 0.50
   States
   {
   Spawn:
      NULL A 3 Bright
      SMK2 ABCDE 1 Bright
      Stop 
   }
}

ACTOR RedSmoke
{   
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   +CLIENTSIDEONLY   
   RENDERSTYLE Add
   ALPHA 0.85
   States
   {
   Spawn:
      NULL A 3 Bright
      FBL1 CDEFG 1 Bright
      Stop 
   }
}

//Moloch's counterattack.  It shoots 12 of these whenever it's stunned.  An effective way of retaliating against groups of smaller targets.
actor MolochScorch
{
	radius 10
	height 14
	damage 10
	speed 40
	DamageType "Fire"
	RenderStyle Add
	alpha 0.90
	Scale 0.90
	Decal RocketScorch
	seesound "moloch/scorch"
	deathsound "moloch/scorchhit"
	PROJECTILE
	+DROPOFF
	+FLOORHUGGER
	+EXTREMEDEATH
	+STRIFEDAMAGE
	states
	{
	Spawn:
		HBFR A 1 A_CStaffMissileSlither
		HBFR B 1 A_CStaffMissileSlither
		HBFR C 1 A_CStaffMissileSlither
		Loop
	Death:
		HBFR DEFGH 5 bright
		stop
	}
}

//Moloch's lightning attack.  It can tear through enemies and shoot lightning bolts.
ACTOR MolochThunderbolt
{
   Radius 18
   Height 12
   Speed 45
   Damage (random(25,50))
   PROJECTILE
   RENDERSTYLE Add
   Alpha 0.90
   Scale 1.3
   Decal BFGScorch
   Damagetype lightning
   +THRUGHOST
   +NODAMAGETHRUST
   +FORCEXYBILLBOARD
   +RIPPER
   +STRIFEDAMAGE
   SeeSound "moloch/thunder"
   Deathsound "moloch/thundercrash"
   States
   {
   Spawn:
      ZAP7 ABCD 1 Bright A_SpawnItemEx("ThunderboltTrail", -(MomX/1.2), -(MomY/1.2), (MomZ/1.2*-1), (MomX/1.2), (MomY/1.2), (MomZ/1.2), -Angle, 138)
      ZAP7 E 0 Bright A_CustomMissile("ThunderFlash",0,0,180,0)
      ZAP7 E 0 Bright A_CustomMissile("ThunderFlash",0,0,90,0)
      ZAP7 E 1 Bright A_CustomMissile("ThunderFlash",0,0,270,0) 
      loop
   Death:
      ZAP7 FGHIJK 4 Bright A_Explode
      stop
   }
}

Actor ThunderboltTrail
{
  Scale 1.1
  Damage 1
  Projectile
  RenderStyle Add
  Alpha 0.8
  +STRIFEDAMAGE
  States
  {
  Spawn:
    ZAP7 AABBCCDDEE 3 Bright A_FadeOut(0.04)
    Loop
  }
}

actor ThunderFlash
{
	radius 6
	height 8
	damage 5
	speed 50
	Renderstyle Add
	Alpha 0.90
	seesound "moloch/flash"
	deathsound "moloch/flashhit"
	PROJECTILE
	+BLOODLESSIMPACT
	states
	{
	Spawn:
		ZAP6 ABC 2 bright
		loop
	Death:
		ZAP1 FEDCBA 1 bright A_Explode (10, 20, 1)
		stop
	}
}

//Moloch's earthquake attack.  This is performed when Moloch dies.  It can do incredible amounts of damage and can kill groups of weak enemies and severely damage strong ones.
ACTOR MolochQuake
{
	Speed 8
	Damage 15
	Radius 12
	Height 16
	RenderStyle Translucent
	Alpha 0.1
	PROJECTILE
	+DROPOFF
	-NOGRAVITY
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	+FloorHugger
	+RIPPER
	SeeSound "moloch/thud"
	States
	{
	Spawn:
		IDGA CCAABBCCC 10 A_Explode
	Death:
		IDGA C 1 A_Explode
		Stop
	}
}

//Moloch's wraith attack.  They are actually monsters that will charge at you and slash you.
ACTOR MolochWraith
{ 
	Health 20
	Radius 12
	Height 28 
	Mass 10 
	Speed 15
	Damage 5
	Damagefactor "Moloch", 0.01
	RENDERSTYLE ADD
	Obituary "%o fell to an unlucky victim of a sacrifice."
	HitObituary "%o was slashed by a red wraith."
	PAINCHANCE 200 
	MONSTER
	+NOGRAVITY 
	+FLOAT
	+DONTFALL
	+DONTHARMSPECIES
	+NOBLOOD
	+NOEXPLODEFLOOR
	+MISSILEMORE
	AttackSound "moloch/wraithattack"
	SeeSound "moloch/wraith"
	PainSound "skull/melee" 
	DeathSound "moloch/wraithdie"
	States 
   { 
   Spawn: 
       UNHE AB 10 A_Look 
       Loop 
   See: 
       UNHE AB 3 A_Chase 
       Loop 
   Missile: 
       UNHE A 10 A_FaceTarget 
       UNHE B 4 A_SkullAttack(45)
       UNHE AB 4 
       Goto Missile+2
   Melee:
       UNHE A 5 A_FaceTarget 
       UNHE B 5 A_CustomMeleeAttack(10,"moloch/wraithmelee","none","Moloch")
       Goto See
   Pain: 
       UNHE A 3  
       UNHE A 3 A_Pain 
       Goto See 
   Death: 
       UNHE H 4 A_ScreamAndUnblock
       UNHE IJKLM 5
       Stop 
   } 
}

ACTOR MolochWraith2
{
  Radius 11
  Height 8
  Speed 5
  Damage 20
  Projectile
  +RANDOMIZE
  SeeSound "moloch/wraith"
  DeathSound "moloch/wraithattack"
  States
  {
  Spawn:
    UNHE A 10 
    UNHE B 4 
    UNHE AB 4 
    Goto Death
  Death:
    UNHE A 1
    TNT1 A 0  A_SetTranslucent (0.8, 0)
    UNHE A 2 
    UNHE B 2 
    UNHE AB 1 
    TNT1 A 0  A_SetTranslucent (0.6, 0)
    UNHE A 2 
    UNHE B 2 
    UNHE AB 1 
    TNT1 A 0  A_SetTranslucent (0.4, 0)
    UNHE A 2 
    UNHE B 2 
    UNHE AB 1 
    TNT1 A 0  A_SetTranslucent (0.2, 0)
    UNHE A 2 
    UNHE B 2 
    UNHE AB 1 
    TNT1 A 0  A_SetTranslucent (0.0, 0)
    Stop
  }
}

//Moloch's nailgun attack.  Moloch can fire flesh-ripping nails in rapid succesion.  They can bounce off walls and floors.
actor MolochNail
{
	radius 2
	height 2
	damage 6
	speed 140
	Scale 0.65
	Decal BulletChip
	Bouncecount 4
	wallbouncefactor 2
	attacksound "moloch/nailhitbleed"
	PROJECTILE
	+RIPPER
	+HEXENBOUNCE
	+SPAWNSOUNDSOURCE
	+EXTREMEDEATH
	+BLOODSPLATTER
	+STRIFEDAMAGE
	states
	{
	Spawn:
		BLAD A 1 bright
		loop
	XDeath:
		SMK2 A 0 A_PlaySound("moloch/nailhitbleed")
		SMK2 ABCDE 2 A_SetTranslucent (0.10,1)
		Stop
	Crash:
		6PUF A 0 A_PlaySound("moloch/nailhit")
		6PUF ABCDEF 2 bright
		Stop
	Death:
		6PUF A 0 A_PlaySound("moloch/nailhit")
		6PUF ABCDEF 2 bright
		Stop
	}
}

actor MolochDeathFire
{
	radius 4
	height 4
	damage 0
	speed 0
	renderstyle add
	alpha 0.90
	Scale 2.0
	seesound "moloch/deathfire"
	PROJECTILE
	+NOCLIP
	+DONTSPLASH
	states
	{
	Spawn:
		XPL2 ABCDEFGHIJK 3 bright A_Explode(1,40)
		stop
	Death:
		NULL A 1 bright
		stop
	}
}
