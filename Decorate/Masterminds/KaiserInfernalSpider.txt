ACTOR KaiserInfernalSpider 7626 
{ 
	//$Category BigSpiders
	Health 5000
	Radius 128
	Height 128
	Mass 1200
	Speed 38
	Scale 2.0
	PainChance 5
	Monster
	+FLOORCLIP
	+BOSS
	+NORADIUSDMG
	+MISSILEMORE
	+DONTHARMSPECIES
    Species "KX"
	SeeSound "baby/sight"
	PainSound "baby/pain"
	DeathSound "baby/death"
	ActiveSound "baby/active"
	bloodcolor "DarkGreen"
	Obituary "%o was eliminated from existance by a Kaiser Infernal Spider."
	States
	{
	Spawn:
		BFSP AB 10 A_Look
		Loop
	See:
		BFSP A 20
		BFSP A 3 A_BabyMetal
		BFSP ABBCC 3 A_Chase
		BFSP D 3 A_BabyMetal
		BFSP DEEFF 3 A_Chase
		Goto See+1
	Missile:
		TNT1 A 0 A_Jump(204,"Bfg", "Repeater", "Chain", "Shotgun")
	Bfg:
		BFSP A 0 A_PlaySound ("weapons/bfgf")
		BFSP A 15 A_FaceTarget
		BFSP G 15 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("BFGBall",32,0,0)
		BFSP G 6
		Goto See+1
	Repeater:
		TNT1 A 0 A_PLaySoundEx("Weapons/RepeaterSpinup", 4, 0, 2)
		BFSP A 16 A_FaceTarget
		Goto FireRepeater
	FireRepeater:
		BFSP HH 0 Bright A_CustomMissile("KaiserInfernalSpiderRepeaterBullet", 32, 0, random(-20, 20), 2, random(-5, 5))
		BFSP H 1 Bright
		BFSP GG 0 Bright A_CustomMissile("KaiserInfernalSpiderRepeaterBullet", 32, 0, random(-20, 20), 2, random(-5, 5))
		BFSP G 1 Bright
		BFSP G 0 Bright A_SpidRefire
		Goto FireRepeater
	Chain:
		BFSP A 15 A_FaceTarget
		BFSP G 15 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("KaiserInfernalSpiderChainBall",32,0,random(-35,35))
		BFSP G 4 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("KaiserInfernalSpiderChainBall",32,0,random(-35,35))
		BFSP G 4 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("KaiserInfernalSpiderChainBall",32,0,random(-35,35))
		BFSP G 4 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("KaiserInfernalSpiderChainBall",32,0,random(-35,35))
		BFSP G 4 A_FaceTarget
		BFSP H 4 bright A_CustomMissile("KaiserInfernalSpiderChainBall",32,0,random(-35,35))
		BFSP G 4 A_FaceTarget
		Goto See+1
	Shotgun:
		BFSP AG 15 A_FaceTarget
		BFSP HHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 0 bright A_CustomMissile("KaiserInfernalSpiderShotgunBall",32,0,random(-35,35))
		BFSP HG 5
		BFSP HHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 0 bright A_CustomMissile("KaiserInfernalSpiderShotgunBall",32,0,random(-35,35))
		BFSP HG 5
		BFSP HHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 0 bright A_CustomMissile("KaiserInfernalSpiderShotgunBall",32,0,random(-35,35))
		BFSP HG 5
		Goto See
	Pain:
		BFSP I 3
		BFSP I 3 A_Pain
		Goto See+1
	Death:
		BFSP J 20 A_Scream
		BFSP K 7 A_Fall
		BFSP LMNO 7
		BFSP P -1
		Stop
	}
}

ACTOR KaiserInfernalSpiderRepeaterBullet
{
	Radius 13
	Height 8
	Speed 50
	Damage (5*Random(1,4))
	Projectile
	+RANDOMIZE
	+THRUSPECIES
	+MTHRUSPECIES
	RenderStyle Add
	Species "KX"
	Decal "BulletChip"
	SeeSound "Weapons/RepeaterFire"
	DeathSound "Weapons/RepeaterHit"
	Alpha 1
	Scale 0.25
	States
	{
	Spawn:
		APLS AB 1 Bright
        Loop
	Death:
	    APBX A 0 A_SetScale(1.0)
		APBX A 0 Bright A_Explode(5, 44, 0)
		APBX AAA 0 Bright A_SpawnItemEx("ArachnotronPlasmaParticle",0,0,0,random(-3,3),random(-3,3),random(-3,3),random(1,360))
        APBX AAAAABBBBBCCCCCDDDDDEEEEE 1 Bright A_FadeOut(0.04)
        Stop
	}
}

Actor KaiserInfernalSpiderChainBall
{
  Radius 13
  Height 8
  Speed 25
  Damage 5
  Projectile
  +RANDOMIZE
  RenderStyle Add 
  Alpha 0.75
  SeeSound "darkinquisitor/darkmatterfire"
  deathsound "weapons/bfgx"
  Translation "112:127=160:167"
  States
  {
  Spawn:
    APLS AB 4 Bright A_SpawnItemEx("KaiserInfernalSpiderChainBall2",0,0,0,0,0,0,0,128)
    Loop
  Death:
    BFE1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BfgParticle",0,0,5,frandom(-7,7),frandom(-7,7),frandom(-7,7),random(1,360))
    BFE1 ABCDEF 4 Bright
    Stop
  }
}

Actor KaiserInfernalSpiderChainBall2 : ArachnotronPlasma
{
  Speed 0
  reactiontime 420
  States
  {
  Spawn:
    APLS AB 5 Bright A_CountDown
    Loop
  }
}

Actor KaiserInfernalSpiderShotgunBall : ArachnotronPlasma
{
  Speed 1
  Translation "112:127=160:167"
  States
  {
  Spawn:
    APLS A 0
    APLS A 0 A_ScaleVelocity(frandom(5,30))
    APLS AB 5 Bright 
    Goto Spawn+2
  }
}
