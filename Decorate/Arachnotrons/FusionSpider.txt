Actor FusionSpider 7577 
{ 
  //$Category KSpiders
  Obituary "%o was vaporized by a fusion cannon Arachnotron"
  Health 600
  Radius 64
  Height 64
  Mass 600
  Speed 12
  PainChance 30   
  SeeSound "baby/sight"
  PainSound "baby/pain"
  DeathSound "baby/death"
  ActiveSound "baby/active"
  Monster
  +FloorClip
  +MissileMore
  +MissileEvenMore
  +DontHarmSpecies
  Species "KX"
  DamageFactor "KX", 0.0
  var int user_fire;
  States
  {
  Spawn:
    FSPI AB 10 A_Look
    Loop
  See:
    FSPI A 20
	FSPI A 0 A_StopSound(CHAN_ITEM)
    FSPI A 3 A_BabyMetal
    FSPI ABBCC 3 A_Chase
    FSPI D 3 A_BabyMetal
    FSPI DEEFF 3 A_Chase
    Goto See+1
  Missile:
    TNT1 A 0 A_JumpIfCloser(624,"CloseDecision")
	TNT1 A 0 A_Jump(256,"Precision","Line")
	Goto See+1
  CloseDecision:
    TNT1 A 0 A_Jump(256,"Precision","Spread","Line")
	Goto See+1
  Precision:
    FSPI A 5 A_FaceTarget
	FSPI G 7 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 7 Bright A_FaceTarget
	FSPI G 6 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 7 Bright A_FaceTarget
    FSPI G 6 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 6 Bright A_FaceTarget
	FSPI G 5 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 6 Bright A_FaceTarget
	FSPI G 5 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 5 Bright A_FaceTarget
	FSPI G 4 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 5 Bright A_FaceTarget
	FSPI G 4 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 4 Bright A_FaceTarget
	FSPI G 4 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 3 Bright A_FaceTarget
	FSPI G 3 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 3 Bright A_FaceTarget
	FSPI G 2 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 3 Bright A_FaceTarget
	FSPI G 2 Bright A_CustomMissile("FusionShot",24,0,frandom(-2,2),2,frandom(-2,2))
    FSPI H 2 Bright A_FaceTarget
	Goto FullCharge
  FullCharge:
	FSPI G 2 Bright A_CustomMissile("FusionShot",24,0,frandom(-4,4),2,frandom(-1,1))
    FSPI H 1 Bright A_FaceTarget
	FSPI H 1 Bright A_SpidRefire
    Loop
  Line: 
	FSPI A 0 A_PlaySound("KXFusionSpider/Alert",CHAN_VOICE,1.0)
	FSPI A 25 A_FaceTarget
	FSPI A 0 A_PlaySound("KXFusionSpider/Line",CHAN_VOICE,1.0)
	FSPI G 0 Bright A_CustomMissile("FusionShot4",24,0,0)
	FSPI GGGGGGGGGGGGGG 0 Bright A_CustomMissile("FusionShot3",24,0,0)
	FSPI GHHH 5 A_FaceTarget
	Goto See+1
  Spread:
    FSPI A 0 A_PlaySound("KXFusionSpider/Charge",CHAN_VOICE,1.0)
	FSPI A 0 A_FaceTarget
	FSPI A 0 A_SetUserVar(user_fire,0)
    FSPI GGGGGGGGGG 1 Bright A_SpawnItemEx("KXFusionSpiderParticle2",0,0,18,random(-4,4),random(-4,4),random(-4,4),random(0,360))
    FSPI A 0 A_FaceTarget
	FSPI GGGGGGGGGG 1 Bright A_SpawnItemEx("KXFusionSpiderParticle2",0,0,18,random(-4,4),random(-4,4),random(-4,4),random(0,360))
    FSPI A 0 A_FaceTarget
	FSPI GGGGGGGGGG 1 Bright A_SpawnItemEx("KXFusionSpiderParticle2",0,0,18,random(-4,4),random(-4,4),random(-4,4),random(0,360))
    FSPI A 0 A_FaceTarget
	FSPI GGGGGGGGGG 1 Bright A_SpawnItemEx("KXFusionSpiderParticle2",0,0,18,random(-4,4),random(-4,4),random(-4,4),random(0,360))
    FSPI A 0 A_FaceTarget
	FSPI GGGGGGGGGG 1 Bright A_SpawnItemEx("KXFusionSpiderParticle2",0,0,18,random(-4,4),random(-4,4),random(-4,4),random(0,360))
    FSPI A 0 A_FaceTarget
	FSPI A 0 A_PlaySound("KXFusionSpider/SpreadRelease",CHAN_WEAPON,1.0)
  LoopingSpread:
    FSPI G 0 A_SetUserVar(user_fire,user_fire+1)
	FSPI GGG 0 A_SpawnItemEx("KXFusionSpiderParticle",0,0,18,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(0,360))
    FSPI G 0 A_PlaySound("KXFusionSpider/Shot2",CHAN_ITEM,1.0,TRUE)
    FSPI G 2 Bright A_CustomMissile("FusionShot2",24,0,random(-50,50),2,frandom(-1,1))
	FSPI G 0 A_FaceTarget
	FSPI HHH 0 A_SpawnItemEx("KXFusionSpiderParticle",0,0,18,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(0,360))
    FSPI H 0 A_PlaySound("KXFusionSpider/Shot2",CHAN_ITEM,1.0,TRUE)
	FSPI H 1 Bright A_CustomMissile("FusionShot2",24,0,random(-50,50),2,frandom(-1,1))
	FSPI H 0 A_FaceTarget
	FAT9 A 0 A_JumpIf(user_fire>25,"See")
    Loop
  Pain:
    FSPI I 3
    FSPI I 3 A_Pain
    Goto See+1
  Death:
    TNT1 A 0 A_SpawnItemEx("KXBloodExplosionEffect",0,0,36,0,0,0,random(1,360))
	TNT1 A 0 A_SpawnItemEx("KXArachnoExplosionDeath",0,0,36,0,0,0,random(1,360))
    FSPI J 20 A_Scream
    FSPI K 7 A_NoBlocking
    FSPI LMNO 7
    FSPI P -1 A_BossDeath
    Stop
  Raise:
    FSPI PONMLKJ 5
    Goto See+1
  }
}

Actor FusionShot
{
  Radius 13
  Height 8
  Speed 30
  Damage (random(5,15)) 
  Scale 0.75
  Scale 1.0   
  RenderStyle Add
  SeeSound "KXFusionSpider/Shot" 
  DeathSound "KXFusionSpider/ShotDeath"
  Projectile
  Decal PlasmaScorchLower
  states
  {
  Spawn:
    FS03 AB 3 Bright A_SpawnItemEx("KXFusionSpiderParticle",FRandom(-3,3),FRandom(-3,3),FRandom(-4,4),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    Loop
  Death:
    TNT1 AAA 0 A_SpawnItemEx("KXFusionSpiderParticle",0,0,4,frandom(-5,5),frandom(-5,5),frandom(-5,5),random(-180,180))
    FS03 HIJG 3 Bright
    Stop
  }
}

Actor FusionShot2 : FusionShot
{
  Speed 1
  SeeSound "" 
  states
  {
  Spawn:
    FS03 A 0
    FS03 A 0 A_ScaleVelocity(frandom(6.0,20.0))
    FS03 AB 4 Bright A_SpawnItemEx("KXFusionSpiderParticle",FRandom(-3,3),FRandom(-3,3),FRandom(-4,4),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    Goto Spawn+2
  }
}

Actor FusionShot3 : FusionShot
{
  Speed 1
  SeeSound "" 
  states
  {
  Spawn:
    FS03 A 0
    FS03 A 0 A_ScaleVelocity(frandom(5.0,25.0))
    FS03 AB 4 Bright A_SpawnItemEx("KXFusionSpiderParticle",FRandom(-3,3),FRandom(-3,3),FRandom(-4,4),frandom(-1,1),frandom(-1,1),frandom(-1,1),random(-180,180))
    Goto Spawn+2
  }
}

Actor FusionShot4 : FusionShot
{
  SeeSound "" 
}

Actor KXFusionSpiderParticle
{
	Projectile
	Damage 0
	+NoBlockMap
	+BloodlessImpact
	+ClientSideOnly
    -BloodSplatter
    +DontSplash
    +ForceXYBillBoard
	Speed 0
	Radius 1
	Height 1
	Scale 1.0
	Alpha 1.00
	RenderStyle Add
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale(ScaleX*0.9)
		FS03 F 1 Bright A_FadeOut(0.05)
		Loop
	}
}

Actor KXFusionSpiderParticle2
{
    +NoInteraction
	+NoBlockMap
	+ClientSideOnly
	Scale 0.75
	RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(AAPTR_DEFAULT,VelX*16,VelY*16,VelZ*16,0,WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET|WARPF_ABSOLUTEANGLE)
        TNT1 A 0 A_ScaleVelocity(-1)
		FS03 FFFFFFFFFFFF 1 Bright
        FS03 FFFF 1 BRIGHT A_FadeOut(0.2)
        Stop
    }
}