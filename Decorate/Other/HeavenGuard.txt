ACTOR HeavenGuard 12345
{
  Health 700
  Radius 20
  Height 56
  Speed 12
  PainChance 100
  Monster
  +AVOIDMELEE
  +FLOORCLIP
  +FRIENDLY
  +NODROPOFF 
  SeeSound "heavenguard/sight"
  PainSound "heavenguard/pain"
  DeathSound "heavenguard/death"
  ActiveSound "heavenguard/active"
  Obituary "%o got his sinful arse thoroughly cleansed by a Heaven Guard."
  DropItem "Cell"
  States
  {
  Spawn:
    HEVN AB 10 A_Look
    Loop
  See:
    HEVN A 0 A_ChangeFlag("FRIGHTENED",0)
    HEVN AABBCCDD 2 A_Chase
    Loop
  Missile:
    HEVN E 4 A_Jump(192,2)
    HEVN E 4 A_JumpIfHealthLower(300, "MissileSuper") // Don't user super attack until damaged at least once.
    HEVN E 4 A_FaceTarget
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN F 3 
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN F 3 
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN E 4
    Goto See
  MissileSuper:
    HEVN E 0 A_PlaySound("heavenguard/attack",CHAN_VOICE)
    HEVN EEEEE 4 A_FaceTarget
    HEVN F 0 BRIGHT A_CustomMissile("HeavenFXSuper1", 24, 0, random(0,0), 0, 0)
    HEVN F 4 BRIGHT A_CustomMissile("HeavenFXSuper2", 32, 0, random(0,0), 0, 0)
    HEVN E 8 
    Goto See
  Pain:
    HEVN A 0 A_Jump(85,2) // 1/3 chance to retreat on hit if damaged enough.
    HEVN A 0 A_JumpIfHealthLower(300, "Retreat")
    HEVN A 2 A_Pain
    // Intentional fall-through.
  Dodge:
    HEVN AAAABBBBCCCCDDDD 1 A_Chase("", "Missile", CHF_FASTCHASE)
	Goto See
  Retreat:
    HEVN A 0 A_PlaySound("heavenguard/retreat",CHAN_VOICE)
    HEVN A 0 A_ChangeFlag("FRIGHTENED",1)
    HEVN AAAABBBBCCCCDDDD 1 A_Chase //("", "Missile", CHF_FASTCHASE)
	Goto Retreat+2
  Death:
    HEVN H 5
    HEVN I 5 A_Scream
    HEVN J 5 A_NoBlocking
    HEVN KLM 5
    HEVN N -1
    Stop
  }
}

ACTOR TurretHeavenGuard 12346
{
  Health 500
  Radius 20
  Height 56
  Speed 0
  PainChance 100
  Monster
  +FLOORCLIP
  +NODROPOFF 
  +FRIENDLY
  SeeSound "heavenguard/sight"
  PainSound "heavenguard/pain"
  DeathSound "heavenguard/death"
  ActiveSound "heavenguard/active"
  Obituary "%o got his sinful arse thoroughly cleansed by a Heaven Guard."
  Species "Heaven"
  States
  {
  Spawn:
    HEVN AB 10 A_Look
    Loop
  See:
    HEVN E 4 A_Chase
    Loop
  Missile:
    HEVN E 4 A_Jump(192,2)
    HEVN E 4 A_JumpIfHealthLower(180, "MissileSuper") 
    HEVN E 4 A_FaceTarget
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN F 3 
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN F 3 
    HEVN F 3 BRIGHT A_CustomMissile("HeavenFX1", 40, 0, random(0,0), 0, 0)
    HEVN E 4
    Goto See
  MissileSuper:
    HEVN E 0 A_PlaySound("heavenguard/attack",CHAN_VOICE)
    HEVN EEEEE 4 A_FaceTarget
    HEVN F 0 BRIGHT A_CustomMissile("HeavenFXSuper1", 24, 0, random(0,0), 0, 0)
    HEVN F 4 BRIGHT A_CustomMissile("HeavenFXSuper2", 32, 0, random(0,0), 0, 0)
    HEVN E 8 
    Goto See
  Pain:
    HEVN A 2 A_Pain
    Goto See
  Death:
    HEVN H 5
    HEVN I 5 A_Scream
    HEVN J 5 A_NoBlocking
    HEVN KLM 5
    HEVN N -1
    Stop
  }
}

// Heaven Guard Projectiles kindly donated by Zero Prophet (based on zp-fetis/HPack D'Sparil Gauntlets projectile).

ACTOR HeavenFX1 : WizardFX1
{
	Obituary "%o got his sinful arse thoroughly cleansed by a Heaven Guard."
	SeeSound "heavenguard/gun1"
	DeathSound "heavenguard/gun2"
	Decal "PlasmaScorchLower"
	Damage 9
	Speed 30
	Alpha 1
	+SEEKERMISSILE
	+THRUSPECIES
	+MTHRUSPECIES
	Species "Marines"
	States
	{
	Spawn:
		HVFX A 0 BRIGHT A_SeekerMissile(5,7)
		HVFX AAA 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX B 0 BRIGHT A_SeekerMissile(5,7)
		HVFX BBB 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		Loop
	Death: 
	    HVT1 AAA 0 bright A_SpawnItemEx("KXHeavenGuardParticleSpawner",0,0,0,frandom(-4,4),frandom(-4,4),frandom(-4,4),random(1,360))
		HVT1 A 1 Bright A_SpawnItemEx("HeavenFX3",0,0,0,0,0,0,0,0)
		HVT1 BCDEFGH 1 Bright
		Stop
	}
}

ACTOR HeavenFX2 : GauntletPuff2
{
    Alpha 0.9
	RenderStyle Add
	+NOINTERACTION
	States
	{
	Spawn:
	    TNT1 A 1
		HVFX CDEFGHIJ 2 BRIGHT
		Stop
	}
}

ACTOR HeavenFX3 : HeavenFX2
{
	Alpha .9
	Scale 1.9
	States
	{
	Spawn:
		HVFX NO 2 BRIGHT
		HVFX PPQQ 1 BRIGHT A_FadeOut(0.15)
		Stop
	}
}

ACTOR HeavenFXSuper1 : HeavenFX1
{
	WeaveIndexXY 0
	WeaveIndexZ 48
	SeeSound "heavenguard/gun1"
	DeathSound "heavenguard/gun5"
	Damage (100*Random(1,1))
	Speed 15
	Scale 1.25
	+SEEKERMISSILE
	+THRUSPECIES
	+MTHRUSPECIES
	Species "Marines"
	States
	{
	Spawn:
		HVFX A 0 A_SeekerMissile(25,25)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX A 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX A 0 A_Weave(4, 4, 0.5, 0.5)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX A 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX A 0 A_Weave(4, 4, 0.5, 0.5)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX A 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX A 0 A_Weave(4, 4, 0.5, 0.5)
		HVFX B 0 A_SeekerMissile(25,25)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX B 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX B 0 A_Weave(4, 4, 0.5, 0.5)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX B 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX B 0 A_Weave(4, 4, 0.5, 0.5)
		HVFX A 0 BRIGHT A_SpawnItemEx("HeavenFX4",0,0,0,0,0,0,0,0)
		HVFX B 1 BRIGHT A_SpawnItemEx("HeavenFX2",random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		HVFX B 0 A_Weave(4, 4, 0.5, 0.5)
		Loop
	Death:
	    HVT1 AAAAAAA 0 bright A_SpawnItemEx("KXHeavenGuardParticleSpawner2",0,0,0,frandom(-10,10),frandom(-10,10),frandom(-10,10),random(1,360))
	    HVFX C 0 A_SetScale(2.0)
		HVFX C 3 BRIGHT A_SpawnItemEx("HeavenFX3",0,0,0,0,0,0,0,0)
		HVFX DEFG 3 BRIGHT
		Stop
	}
}

ACTOR HeavenFXSuper2 : HeavenFXSuper1
{
	WeaveIndexXY 32
	WeaveIndexZ  16 
}

ACTOR HeavenFX4 : HeavenFX2
{
	VSpeed 0
	Scale 1.25
	Alpha 0.5
	States
	{
	Spawn:
		HVFX AABBAABB 1 A_FadeOut(0.05)
		Stop
	}
}

Actor KXHeavenGuardParticleSpawner
{
	Projectile
	Damage 0
	+NOBLOCKMAP
	+BLOODLESSIMPACT
	+CLIENTSIDEONLY 
	+NoBlockMap
    -BLOODSPLATTER
    +DontSplash
    +FORCEXYBILLBOARD
	+DoomBounce
	BounceCount 1
	Speed 0
	Radius 1
	Height 1
	Scale 0.15
	RenderStyle Add
	States
	{
  Spawn:
    TNT1 AAAAAAAAAA 2 Bright A_SpawnItemEx("KXHeavenGuardParticleTrail",0,0,0,frandom(-1,1),frandom(-1,1),frandom(-1,1),random(1,360), SXF_TRANSFERTRANSLATION)
    Stop
	}
}

Actor KXHeavenGuardParticleSpawner2 : KXHeavenGuardParticleSpawner
{
  States
  {
  Spawn:
    TNT1 AAAAAAAAAAAAAAAAAAAA 1 Bright A_SpawnItemEx("KXHeavenGuardParticleTrail",0,0,0,frandom(-1,1),frandom(-1,1),frandom(-1,1),random(1,360), SXF_TRANSFERTRANSLATION)
    Stop
	}
}

Actor KXHeavenGuardParticleTrail : HeavenFX2
{
	+NOBLOCKMAP
	+NOINTERACTION
    +CLIENTSIDEONLY
	States
	{
	Spawn:
    HVFX L 1 Bright A_FadeOut(0.05)
    Loop
	}
}